definition {

	macro _addJSONArray {
		if (!(isSet(json)) || !(isSet(key))) {
			fail("Error, 'JSON' and 'key' must all be set");
		}

		// Check if object already exists

		var bool = JSONUtil2._isJSONArrayPresent(
			json = ${json},
			key = ${key});

		if (${bool} == "TRUE") {
			echo("JSON array  ${key}' already added, returning original JSON");
		}
		else {
			if (${json} == "{}") {
				var json = '''{"${key}" : []}''';
			}
			else {
				var json = StringUtil.regexReplaceFirst(${json}, "(.*)\}", "$1, \"${key}\": []}");
			}
		}

		return ${json};
	}

	macro _addJSONArrayValues {
		if (!(isSet(json)) || !(isSet(key)) || !(isSet(values))) {
			fail("Error, 'JSON', 'key', and 'values' must all be set");
		}

		var bool = JSONUtil2._isJSONArrayPresent(
			json = ${json},
			key = ${key});

		if (${bool} == "FALSE") {
			fail("Error, '${key}' does not exist in the JSON");
		}

		for (var value : list ${values}) {
			var temp = RegexUtil.replace(${json}, "\"${key}\"\s*:\s*(\[.*?\])", 1);

			if (contains("\"${temp}\"", "\"${value}\"")) {
				fail("'${value}' already exists inside 'JSON'");
			}

			if (${temp} == "[]") {

				// If empty no need to add comma

				var json = StringUtil.regexReplaceFirst(${json}, "(\"${key}\"\s*:\s*)\[\]", "$1[\"${value}\"]");
			}
			else {
				var json = StringUtil.regexReplaceFirst(${json}, "(\"${key}\"\s*:\s*)(\[.*?)\]", "$1$2, \"${value}\"]");
			}
		}

		return ${json};
	}

	macro _addJSONObject {
		if (!(isSet(json)) || !(isSet(key)) || !(isSet(value))) {
			fail("Error, 'JSON', 'key', and 'value' must all be set");
		}

		var bool = JSONUtil2._isJSONObjectPresent(
			json = ${json},
			key = ${key});

		if (${bool} == "TRUE") {
			fail("Error, '${key}' already exists in the JSON");
		}

		if (${json} == "{}") {
			var json = '''{"${key}" : ${value}}''';
		}
		else {
			var json = StringUtil.regexReplaceFirst(${json}, "(.*)\}", "$1, \"${key}\": ${value}}");
		}

		return ${json};
	}

	macro _convertLocalizedListToMap {

		// Create list using '${listMap'

		var localizedMap = "{";

		for (var i : list ${listMap}) {
			var locale = StringUtil.extractFirst(${i}, ":");
			var translated = StringUtil.extractLast(${i}, ":");

			var localizedMap = '''${localizedMap}"${locale}":"${translated}",''';
		}

		// Replace last comma with }

		var localizedMap = RegexUtil.replace(${localizedMap}, "(.*)(?=,)", 1);

		var localizedMap = "${localizedMap}}";

		return ${localizedMap};
	}

	macro _exclusiveOrCheck {
		if ((isSet(a) && isSet(b)) || (!(isSet(b)) && !(isSet(a)))) {
			fail(${failMessage});
		}
	}

	macro _isJSONArrayPresent {
		var temp = RegexUtil.replace(${json}, "\"(${key})\"\s*:\s*\[.*?\]", 1);

		if (${temp} == ${key}) {
			return "TRUE";
		}
		else {
			return "FALSE";
		}
	}

	macro _isJSONObjectPresent {
		var temp = RegexUtil.replace(${json}, "\"(${key})\"\s*:\s*", 1);

		if (${temp} == ${key}) {
			return "TRUE";
		}
		else {
			return "FALSE";
		}
	}

	macro formatJSONSuperAdminUser {
		var superAdminUserEmail = JSONUtil2.getSuperAdminUserEmail();
		var superAdminUserPassword = JSONUtil2.getSuperAdminUserPassword();

		return "${superAdminUserEmail}:${superAdminUserPassword}";
	}

	macro formatJSONUser {
		var defaultUserEmail = JSONUtil2.getDefaultUserEmail(domain = ${domain});
		var defaultUserPassword = JSONUtil2.getDefaultUserPassword();

		return "${defaultUserEmail}:${defaultUserPassword}";
	}

	macro getDefaultUserEmail {
		if (isSet(domain)) {
			var portalInstanceName = JSONCompany.parsePortalInstanceURL(portalURL = ${domain});

			var customDefaultUserEmail = "test@${portalInstanceName}";
		}
		else {
			var customDefaultUserEmail = PropsUtil.get("custom.default.user.email");
		}

		if (!(isSet(customDefaultUserEmail))) {
			var customDefaultUserEmail = "test@liferay.com";
		}

		return ${customDefaultUserEmail};
	}

	macro getDefaultUserFirstPassword {
		var customDefaultUserFirstPassword = PropsUtil.get("custom.default.user.first.password");

		if (!(isSet(customDefaultUserFirstPassword))) {
			var customDefaultUserFirstPassword = "test";
		}

		return ${customDefaultUserFirstPassword};
	}

	macro getDefaultUserPassword {
		var customDefaultUserPassword = PropsUtil.get("custom.default.user.password");

		if (!(isSet(customDefaultUserPassword))) {
			var customDefaultUserPassword = "test";
		}

		return ${customDefaultUserPassword};
	}

	macro getLXCEnvironment {
		var lxcEnvironment = PropsUtil.get("lxc.environment");

		if (!(isSet(lxcEnvironment))) {
			var lxcEnvironment = "false";
		}

		return ${lxcEnvironment};
	}

	macro getSuperAdminUserEmail {
		var customSuperAdminUserEmail = PropsUtil.get("custom.super.admin.user.email");

		if (!(isSet(customSuperAdminUserEmail))) {
			var customSuperAdminUserEmail = "test@liferay.com";
		}

		return ${customSuperAdminUserEmail};
	}

	macro getSuperAdminUserPassword {
		var customSuperAdminUserPassword = PropsUtil.get("custom.super.admin.user.password");

		if (!(isSet(customSuperAdminUserPassword))) {
			var customSuperAdminUserPassword = "test";
		}

		return ${customSuperAdminUserPassword};
	}

}