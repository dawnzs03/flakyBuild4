@component-name = "portal-portal-configuration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Virtual Instances";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			PortalInstances.tearDownCP();

			PagesAdmin.tearDownCP();
		}
	}

	@priority = 5
	test CanAddAndDeletePortalInstance {
		property portal.acceptance = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.viewCP(virtualHost = "www.able.com");

		PortalInstances.deleteCP(virtualHost = "www.able.com");
	}

	@description = "This automates LPS-157752. It asserts that email settings can be configured in a Virtual Instance."
	@priority = 4
	test CanConfigureEmailSettingsInVirtualInstance {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given a new Virtual Instance is created") {
			ApplicationsMenu.gotoPortlet(
				category = "System",
				panel = "Control Panel",
				portlet = "Virtual Instances");

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				virtualHost = "www.able.com",
				webId = "www.able.com");

			User.firstLoginPG(
				password = "test",
				userEmailAddress = "test@www.able.com",
				virtualHostsURL = "http://www.able.com:8080");
		}

		task ("When a user makes changes to email settings in the Virtual Instance and saves") {
			PortalSettings.gotoConfiguration(
				baseURL = "http://www.able.com:8080",
				configurationCategory = "Email",
				configurationName = "Mail Settings",
				configurationScope = "Virtual Instance Scope");

			FormFields.editText(
				fieldName = "pop3Port",
				fieldValue = 995);

			PortletEntry.save();
		}

		task ("Then the new configuration should now be displayed") {
			FormFields.viewTextValue(
				fieldName = "pop3Port",
				fieldValue = 995);
		}
	}

	@description = "This is a use case for LPS-161481. Verify a non-omniadmin user can create a user in a user-limited virtual instance"
	@priority = 3
	test NonAdminUserCanCreateUserInUserLimitedVirtualInstance {
		property test.assert.warning.exceptions = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		task ("Given a user limited virtual instance") {
			PortalInstances.openVirtualInstancesAdmin();

			PortalInstances.addCP(
				mailDomain = "www.able.com",
				maxUserNumber = 4,
				maxUsers = "true",
				virtualHost = "www.able.com",
				webId = "www.able.com");

			User.firstLoginUI(
				password = "test",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "test@www.able.com");
		}

		task ("And given a user exists in that instance with permission to create other users") {
			JSONRole.addRegularRole(
				roleTitle = "Test Role",
				specificURL = "http://www.able.com:8080");

			Permissions.definePermissionsViaJSONAPI(
				resourceNameActionList = "com_liferay_users_admin_web_portlet_UsersAdminPortlet,ACCESS_IN_CONTROL_PANEL,VIEW:com.liferay.portal.kernel.model.User,DELETE,UPDATE,VIEW:90,VIEW_CONTROL_PANEL,ADD_USER",
				roleTitle = "Test Role",
				roleType = "regular",
				specificURL = "http://www.able.com:8080");

			JSONUser.addUser(
				creatorEmailAddress = "test@www.able.com",
				creatorPassword = "test",
				portalInstanceName = "www.able.com",
				specificURL = "http://www.able.com:8080",
				userEmailAddress = "userea@www.able.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			User.openUsersAdmin(baseURL = "http://www.able.com:8080");

			User.assignRegularRoleCP(
				roleTitle = "Test Role",
				userScreenName = "usersn");

			User.editPassword(newPassword = "test");
		}

		task ("Then that user can create another user in the instance without error") {
			User.logoutPG();

			SignIn._signIn(
				userEmailAddress = "userea@www.able.com",
				virtualHostsURL = "http://www.able.com:8080");

			User.openUsersAdmin(baseURL = "http://www.able.com:8080");

			User.addCP(
				userEmailAddress = "newuser@liferay.com",
				userFirstName = "new",
				userLastName = "user",
				userScreenName = "newuser");
		}
	}

	@priority = 4
	test PermissionCheckPortalInstances {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		PortalInstances.addCP(
			mailDomain = "www.baker.com",
			virtualHost = "www.baker.com",
			webId = "www.baker.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media");

		DMDocument.addCP(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.doc",
			dmDocumentTitle = "DM Document Title");

		DMNavigator.gotoDocumentCP(dmDocumentTitle = "DM Document Title");

		DMDocument.viewCP(dmDocumentTitle = "DM Document Title");

		var documentURL = selenium.getLocation();

		var otherInstanceAccessAttempt = StringUtil.replace(${documentURL}, "able", "baker");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.baker.com",
			virtualHostsURL = "http://www.baker.com:8080");

		Navigator.openSpecificURL(url = ${otherInstanceAccessAttempt});

		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "You do not have the required permissions.");
	}

	@priority = 5
	test ViewDeactivatedPortalInstance {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Infrastructure",
			configurationName = "Inactive Request Handler",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "Show Inactive Request Message");

		SystemSettings.saveConfiguration();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Blogs Page");

		Navigator.gotoPage(pageName = "Blogs Page");

		Portlet.addPG(portletName = "Blogs");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Wiki Page");

		Navigator.gotoPage(pageName = "Wiki Page");

		Portlet.addPG(portletName = "Wiki");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Language Page");

		Navigator.gotoPage(pageName = "Language Page");

		Portlet.addPG(portletName = "Language Selector");

		User.logoutPG();

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		Page.viewNoPG(pageName = "Blogs Page");

		Page.viewNoPG(pageName = "Wiki Page");

		Page.viewNoPG(pageName = "Language Page");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		// Assert the Home page is a content page in the new virtual instance

		AssertVisible(locator1 = "Icon#EDIT_PENCIL");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(pageName = "Web Content Page");

		Navigator.gotoSpecificURLPage(
			pageName = "Web Content Page",
			url = "http://www.able.com:8080");

		Portlet.addPG(portletName = "Web Content Display");

		Navigator.gotoSpecificURLPage(
			pageName = "Web Content Page",
			url = "http://www.able.com:8080");

		Portlet.addPG(portletName = "IFrame");

		User.logoutPG();

		User.loginPG();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.deactivatePortalInstance(virtualHost = "www.able.com");

		Navigator.openSpecificURL(url = "http://www.able.com:8080");

		Page.viewInactiveRequestMessage(resource = "instance");
	}

	@ignore = "true"
	@priority = 4
	test ViewVirtualHostPrivateSitePage {
		property custom.properties = "virtual.hosts.valid.hosts=localhost,127.0.0.1,www.able.com,www.baker.com,www.charlie.com";
		property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		PortalSettings.gotoConfiguration(
			configurationCategory = "Instance Configuration",
			configurationName = "General",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editVirtualHostCP(virtualHostName = "www.able.com");

		Site.openSiteSettingsAdmin(siteURLKey = "guest");

		Site.addVirtualHostsURLCP(
			publicPageVirtualHostURL = "www.baker.com",
			publicVirtualHosts = ${publicPageVirtualHostURL});

		Site.openSiteSettingsAdmin(siteURLKey = "guest");

		Site.addVirtualHostsURLCP(
			privatePageVirtualHostURL = "www.charlie.com",
			privateVirtualHosts = ${privatePageVirtualHostURL});

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPublicPage(pageName = "Public Page");

		PagesAdmin.addPrivatePage(pageName = "Private Page");

		Navigator.openSpecificURL(url = "http://www.charlie.com:8080");

		AssertElementPresent(
			key_pageName = "Home",
			locator1 = "Home#PAGE");

		User.loginPG(
			password = "test",
			specificURL = "http://www.charlie.com:8080/group/guest",
			userEmailAddress = "test@liferay.com");

		AssertTextEquals(
			key_pageName = "Private Page",
			locator1 = "Home#PAGE",
			value1 = "Private Page");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@priority = 4
	test ViewVirtualInstanceInheritsSystemSettings {
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		PortalSettings.gotoConfiguration(
			configurationCategory = "Users",
			configurationName = "Personal Menu",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.viewToggleSwitchNotChecked(portalSetting = "Show in Control Menu");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Users",
			configurationName = "Personal Menu",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.editConfigurationUsersPersonalMenuShowInControlMenu(toggleSwitchLabel = "Show in Control Menu");

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Virtual Instances");

		PortalInstances.addCP(
			mailDomain = "www.able.com",
			virtualHost = "www.able.com",
			webId = "www.able.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@www.able.com",
			virtualHostsURL = "http://www.able.com:8080");

		PortalSettings.gotoConfiguration(
			configurationCategory = "Users",
			configurationName = "Personal Menu",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.viewToggleSwitchChecked(portalSetting = "Show in Control Menu");
	}

}