@component-name = "portal-search"
definition {

	property portal.release = "true";
	property portal.suite.search.engine = "elasticsearch7";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Search";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-182927 and LPS-164852."
	@priority = 4
	test AssertURLforSuggestionsResults {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "New Search Page");

		Navigator.gotoPage(pageName = "New Search Page");

		SearchBarSuggestions.viewSuggestionResultDetailsURL(
			expectedURL = "/new-search-page",
			searchAssetTitle = "Apple");

		Navigator.gotoBack();

		var currentURL = selenium.getCurrentUrl();

		if (!(contains(${currentURL}, "/new-search-page"))) {
			fail("Clicking the in-page back button should take the user to the page they came from");
		}

		SearchPortlets.configureEmbeddedSearchBar(destinationPage = "New Search Page");

		SearchBarSuggestions.viewSuggestionResultDetailsURL(
			expectedURL = "/new-search-page",
			searchAssetTitle = "Apple");

		SearchPortlets.configureEmbeddedSearchBar(destinationPage = "");

		SearchBarSuggestions.viewSuggestionResultDetailsURL(
			expectedURL = "/new-search-page",
			searchAssetTitle = "Apple");
	}

	@priority = 4
	test CanConfigureSearchContributorSize {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple Pie");

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
			suggestionContributorOrder = 1,
			suggestionContributorSize = 1);

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie");
	}

	@priority = 4
	test CanCopyPasteInputFields {
		SearchPortlets.gotoEmbeddedOptions(portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Click.mouseDownMouseUp(
			key_text = "Add Suggestions",
			locator1 = "Button#ANY");

		Click(
			key_suggestionContributorOrder = 1,
			locator1 = "SearchBarSuggestions#SUGGESTION_CONTRIBUTOR");

		Click(
			key_suggestionContributor = "Blueprint",
			key_suggestionContributorOrder = 1,
			locator1 = "SearchBarSuggestions#SUGGESTION_CONTRIBUTOR_SELECT");

		Click.clickAt(
			key_suggestionContributorOrder = 1,
			locator1 = "SearchBarSuggestions#SUGGESTION_CONTRIBUTOR_DISPLAY_GROUP_NAME");

		SearchBarSuggestions.copyConfiguration(
			contributorOrder = 1,
			suggestionContributor = "Display Group Name");

		SearchBarSuggestions.pasteConfiguration(
			contributorOrder = 2,
			suggestionContributor = "Display Group Name");

		AssertVisible(
			key_suggestionContributorOrder = 2,
			locator1 = "SearchBarSuggestions#SUGGESTION_CONTRIBUTOR_DISPLAY_GROUP_NAME",
			value1 = "suggestions");

		SearchBarSuggestions.copyConfiguration(
			contributorOrder = 1,
			suggestionContributor = "Size");

		SearchBarSuggestions.pasteConfiguration(
			contributorOrder = 2,
			suggestionContributor = "Size");

		AssertVisible(
			key_suggestionContributorOrder = 2,
			locator1 = "SearchBarSuggestions#SUGGESTION_CONTRIBUTOR_SIZE",
			value1 = 5);
	}

	@priority = 4
	test CanViewBasicAndBlueprintSuggestionsTogether {
		property test.run.environment = "EE";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		Blueprints.openBlueprintsAdmin();

		Blueprints.createBlueprint(title = "Empty Blueprint");

		Navigator.openURL();

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
			addContributor = "true",
			suggestionContributor = "Blueprint",
			suggestionContributorBlueprint = "Empty Blueprint",
			suggestionContributorDisplayGroupName = "blueprint suggestions",
			suggestionContributorOrder = 2,
			suggestionContributorSize = 5);

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple",
			suggestionContributorDisplayGroupName = "suggestions");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple",
			suggestionContributorDisplayGroupName = "blueprint suggestions");
	}

	@priority = 4
	test CanViewSearchBarSuggestionsResultDetails {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple Pie");

		SearchBarSuggestions.showMoreSuggestions();

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Apple",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Apple Pie",
			searchAssetType = "Document");

		Type(
			locator1 = "Search#SEARCH_BAR_WIDGET",
			value1 = "Apple");

		SearchBarSuggestions.gotoSuggestionResultDetails(searchAssetTitle = "Apple Pie");

		SearchBarSuggestions.viewSuggestionResultDetails(searchAssetTitle = "Apple Pie");
	}

	@description = "This is a use case for LPS-166050. When using a Blueprint to drive Search Bar suggestions, I can configure a character threshold that applies to this group"
	@priority = 3
	test ConfigureBlueprintContributorCharacterThresholds {
		task ("Add Web Content and Blueprint") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Guest",
				title = "Pineapple");

			Blueprints.openBlueprintsAdmin();

			Blueprints.createBlueprint(title = "Empty Blueprint");
		}

		Navigator.gotoPage(pageName = "Home");

		task ("Add and configure Contributors") {
			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				characterThreshold = 1,
				suggestionContributor = "Blueprint",
				suggestionContributorBlueprint = "Empty Blueprint",
				suggestionContributorDisplayGroupName = "Blueprint 1",
				suggestionContributorOrder = 2,
				suggestionContributorSize = 2);

			Refresh();

			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				characterThreshold = 3,
				suggestionContributor = "Blueprint",
				suggestionContributorBlueprint = "Empty Blueprint",
				suggestionContributorDisplayGroupName = "Blueprint 3",
				suggestionContributorOrder = 3,
				suggestionContributorSize = 2);

			Refresh();

			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				characterThreshold = 5,
				suggestionContributor = "Blueprint",
				suggestionContributorBlueprint = "Empty Blueprint",
				suggestionContributorDisplayGroupName = "Blueprint 5",
				suggestionContributorOrder = 4,
				suggestionContributorSize = 2);
		}

		task ("Assert that 'Blueprint 1' shows up as a display group and 'Pineapple' appears as a suggestion in that group") {
			Type(
				locator1 = "Search#EMBEDDED_SEARCH_BAR",
				value1 = "p");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "Pineapple",
				suggestionContributorDisplayGroupName = "Blueprint 1");
		}

		task ("Assert that 'Blueprint 3' shows up as a display group and 'Pineapple' appears as a suggestion in that group") {
			Type(
				locator1 = "Search#EMBEDDED_SEARCH_BAR",
				value1 = "pin");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "Pineapple",
				suggestionContributorDisplayGroupName = "Blueprint 3");
		}

		task ("Assert that 'Blueprint 5' shows up as a display group and 'Pineapple' appears as a suggestion in that group") {
			Type(
				locator1 = "Search#EMBEDDED_SEARCH_BAR",
				value1 = "pinea");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "Pineapple",
				suggestionContributorDisplayGroupName = "Blueprint 5");
		}
	}

	@description = "This is a use case for LPS-164683."
	@ignore = "true"
	@priority = 4
	test ConfigureSearchBarToShowTopAndTrendingSearchesUsingDataFromAnalyticsCloud {
		property custom.properties = "feature.flag.LPS-159643=true";
		property property.group = "analytics.cloud.profile";

		task ("Configure Analytics Cloud") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();

			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();

			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Guest");
		}

		task ("Add Web Content Articles to search") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				description = "WC WebContent Description",
				groupName = "Guest",
				title = "Apple Pie");

			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				description = "WC WebContent Description",
				groupName = "Guest",
				title = "Apple Strudel");
		}

		task ("Configure Site Activities suggestions for embedded Search Bar") {
			Navigator.openURL();

			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				suggestionContributor = "Site Activities",
				suggestionContributorActivity = "Top Searches",
				suggestionContributorMinimumSearches = 0,
				suggestionContributorOrder = 2,
				suggestionContributorSize = 1);

			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				suggestionContributor = "Site Activities",
				suggestionContributorActivity = "Trending Searches",
				suggestionContributorMinimumSearches = 0,
				suggestionContributorOrder = 3);
		}

		task ("Populate AC with data") {
			SearchPortlets.searchEmbedded(searchTerm = "Apple Pie");

			SearchResultPortlet.viewSearchResults(
				searchAssetTitle = "Apple Pie",
				searchAssetType = "Web Content Article");

			SearchPortlets.searchEmbedded(searchTerm = "Apple Pie");

			SearchResultPortlet.viewSearchResults(
				searchAssetTitle = "Apple Pie",
				searchAssetType = "Web Content Article");

			SearchPortlets.searchEmbedded(searchTerm = "Apple Strudel");

			SearchResultPortlet.viewSearchResults(
				searchAssetTitle = "Apple Strudel",
				searchAssetType = "Web Content Article");

			// Analytics Cloud needs up to 3 minutes to track searches. See LPS-164683 for details.

			Pause.pauseNoSPARefresh(locator1 = 180000);
		}

		task ("Assert search results show up under the expected suggestion contributor display groups") {
			Navigator.openURL();

			Type(
				locator1 = "Search#EMBEDDED_SEARCH_BAR",
				value1 = "apple");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "apple pie",
				suggestionContributorDisplayGroupName = "Top Searches");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "apple pie",
				suggestionContributorDisplayGroupName = "Trending Searches");

			SearchBarSuggestions.viewSuggestions(
				searchAssetTitle = "apple strudel",
				suggestionContributorDisplayGroupName = "Trending Searches");
		}
	}

	@description = "This is a use case for LPS-188710 - Configure Site Activities suggestions to display results from the user's locale."
	@ignore = "true"
	@priority = 3
	test ConfigureSuggestionsToDisplayResultsFromUsersLocale {
		property custom.properties = "feature.flag.LPS-159643=true";
		property property.group = "analytics.cloud.profile";

		task ("Configure Analytics Cloud") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();

			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();

			var propertyName = ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Guest");
		}

		task ("Create Apple Pie and Apple Strudel Web Contents and translate Apple Strudel") {
			JSONWebcontent.addWebContent(
				content = "Apple Pie content",
				groupName = "Guest",
				title = "Apple Pie");

			JSONWebcontent.addBasicWebContentLocalized(
				contentMap = "en_US:Apple Strudel content,pt_BR:Conteúdo de Strudel de Maçã",
				groupName = "Guest",
				titleMap = "en_US:Apple Strudel,pt_BR:Strudel de Maçã");
		}

		task ("Configure Embedded Search Bar Suggestions to show the trending searches") {
			SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
				addContributor = "true",
				characterThreshold = 0,
				suggestionContributor = "Site Activities",
				suggestionContributorActivity = "Trending Searches",
				suggestionContributorOrder = 2,
				suggestionContributorSize = 3);
		}

		task ("Click on Embedded Search Bar and assert Apple Pie and Apple Strudel results") {
			Click(locator1 = "Search#EMBEDDED_SEARCH_BAR");

			SearchBarSuggestions.viewSuggestions(searchAssetTitle = "Apple Pie");

			SearchBarSuggestions.viewSuggestions(searchAssetTitle = "Apple Strudel");
		}

		task ("Set language to match translation language and assert Apple Strudel translated result") {
			Search.openSearchPage(locale = "pt-BR");

			Navigator.openURL();

			Click(locator1 = "Search#EMBEDDED_SEARCH_BAR");

			SearchBarSuggestions.viewSuggestions(searchAssetTitle = "Strudel de Maçã");
		}
	}

	@description = "This is a use case for LPS-155317"
	@priority = 4
	test DisablingSuggestionsSAPHidesSuggestionsFromGuests {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Service Access Policy");

		ServiceAccessPolicy.editServiceAccessPolicy(
			enabled = "false",
			policyName = "SEARCH_SUGGESTIONS");

		Navigator.openURL();

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SignOut.signOut();

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple");

		SignIn.signIn(userEmailAddress = "test@liferay.com");
	}

	@priority = 4
	test DisplaySuggestionsAfterCharacterThresholdIsMet {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(characterThresholdForDisplayingSuggestions = 3);

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Ap");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");
	}

	@description = "This is a use case for LPS-162509."
	@priority = 4
	test HonorLetUserChooseSiteScope {
		JSONWebcontent.addWebContent(
			content = "Content",
			description = "Apple WC Description",
			groupName = "Guest",
			title = "Apple Web Content");

		JSONWebcontent.addWebContent(
			content = "Content ",
			description = "Banana WC Description",
			groupName = "Global",
			title = "Banana Web Content");

		SearchPortlets.configureEmbeddedSearchBar(searchScope = "Let the User Choose");

		SearchPortlets.chooseEmbeddedScope(searchScope = "Everything");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Web Content");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "Apple WC Description",
			searchAssetTitle = "Apple Web Content");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "Banana WC Description",
			searchAssetTitle = "Banana Web Content");

		Refresh();

		SearchPortlets.chooseEmbeddedScope(searchScope = "This Site");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Web Content");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Banana Web Content");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "Apple WC Description",
			searchAssetTitle = "Apple Web Content");
	}

	@description = "This is a use case for LPS-176871."
	@priority = 4
	test SearchSuggestionsInNonRootContext {
		property portal.context = "liferay";
		property search.environment.suite.exclude = "true";
		property skip.clean-app-server-deploy-dir = "true";
		property test.assert.warning.exceptions = "true";
		property test.liferay.virtual.instance = "false";
		property test.run.type = "single";

		JSONWebcontent.addWebContent(
			content = "Web Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		AssertJavaScriptErrors();
	}

	@priority = 4
	test ShowHTMLAsPlainText {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Test Content Page Name",
			type = "content");

		JSONLayout.publishLayout(
			groupName = "Guest",
			layoutName = "Test Content Page Name");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "test");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "Page: Test Content Page Name",
			searchAssetTitle = "Test Content Page Name",
			suggestionContributorDisplayGroupName = "Suggestions");
	}

	@priority = 5
	test ViewBasicSuggestionsAcrossDifferentScopes {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		HeadlessSite.addSite(siteName = "Site Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie",
			groupName = "Site Name",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie");

		Refresh();

		SearchPortlets.configureEmbeddedSearchBar(searchScope = "Everything");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple Pie");
	}

	@priority = 3
	test ViewBlueprintContributorZeroCharacterThreshold {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Guest",
			title = "Software");

		Blueprints.openBlueprintsAdmin();

		Blueprints.createBlueprint(title = "Test Blueprint");

		Blueprints.addElement(element = "Text Match Over Multiple Fields");

		Type(
			ariaLabel = "Text to Match",
			locator1 = "TextInput#ANY_WITH_AIRA_LABEL",
			value1 = "software");

		PortletEntry.save();

		Navigator.gotoPage(pageName = "Home");

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
			addContributor = "true",
			characterThreshold = 0,
			suggestionContributor = "Blueprint",
			suggestionContributorBlueprint = "Test Blueprint",
			suggestionContributorDisplayGroupName = "Blueprint 0",
			suggestionContributorOrder = 2,
			suggestionContributorSize = 5);

		Click(locator1 = "Search#EMBEDDED_SEARCH_BAR");

		SearchBarSuggestions.viewSuggestions(
			searchAssetTitle = "Software",
			suggestionContributorDisplayGroupName = "Blueprint 0");
	}

	@priority = 5
	test ViewBlueprintSuggestionsAcrossDifferentScopes {
		property test.run.environment = "EE";

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple Guest");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie Guest",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		HeadlessSite.addSite(siteName = "Site Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Site Name",
			title = "Apple Site Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie Site Name",
			groupName = "Site Name",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Blueprints.openBlueprintsAdmin();

		Blueprints.createBlueprint(title = "Test Blueprint");

		Navigator.gotoNavNested(navNested = "Query Settings");

		Check.checkNotVisible(
			locator1 = "Radio#ANY",
			radioLabel = "Selected Types");

		Button.click(button = "Select Asset Types");

		Check.checkNotVisible(
			key_assetType = "Web Content Article",
			locator1 = "Blueprints#BLUEPRINTS_SEARCHABLE_ASSET_TYPE_CHECKBOX");

		Button.clickDone();

		PortletEntry.save();

		Navigator.openURL();

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
			addContributor = "true",
			deleteBasicContributor = "true",
			suggestionContributor = "Blueprint",
			suggestionContributorBlueprint = "Test Blueprint",
			suggestionContributorDisplayGroupName = "Suggestions",
			suggestionContributorSize = 3);

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple Guest");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie Guest");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Site Name");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie Site Name");

		SearchPortlets.configureEmbeddedSearchBar(searchScope = "Everything");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple Guest");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie Guest");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple Site Name");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie Site Name");
	}

	@description = "This is a use case for LPS-192650 - HTML seen in search suggestions widget."
	@priority = 2
	test ViewSearchSuggestionsWidgetDontDisplayHTML {
		property test.liferay.virtual.instance = "false";

		task ("Add Suggestions Widget, go to Search Page and configure it to enable the 'Display Did you mean: ...'") {
			SearchPortlets.addWidgets(searchPortletList = "Suggestions");

			Search.openSearchPage();

			SearchPortlets.gotoConfigureSuggestionsOptions(portletOption = "Configuration");

			SelectFrame(locator1 = "IFrame#CONFIGURATION");

			FormFields.enableCheckbox(fieldName = "Did you mean: ...");

			PortletEntry.save();

			IFrame.closeFrame();
		}

		task ("Reindex Reindex Spell-Check Dictionaries, go to Home Page, search for 'lifewayz' and assert 'lifewai' is shown") {
			SearchAdministration.executeReindex(action = "Reindex Spell-Check Dictionaries");

			Navigator.openURL();

			SearchPortlets.searchEmbedded(searchTerm = "lifewayz");

			AssertElementPresent(locator1 = "//div[contains(@class, 'search-suggested-spelling')]//*[contains(text(),'lifewai')]");
		}
	}

	@description = "This is a use case for LPS-190910."
	@priority = 3
	test ViewSuggestionsLocalizedDescription {
		JSONWebcontent.addBasicWebContentLocalized(
			contentMap = "en_US:content,es_ES:contenido,de_DE:inhalt",
			descriptionMap = "en_US:description,es_ES:descripción",
			groupName = "Guest",
			titleMap = "en_US:Title,es_ES:Título,de_DE:Titel");

		Search.openSearchPage(locale = "es-ES");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Title");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "descripción",
			searchAssetTitle = "Título",
			suggestionContributorDisplayGroupName = "Sugerencias");

		AssertElementNotPresent(
			key_searchAssetDescription = "description",
			key_searchAssetTitle = "Título",
			key_suggestionContributorDisplayGroupName = "Sugerencias",
			locator1 = "SearchBarSuggestions#RESULTS_LIST_ITEM_DESCRIPTION");

		Search.openSearchPage(locale = "de-DE");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Title");

		SearchBarSuggestions.viewSuggestions(
			searchAssetTitle = "Titel",
			suggestionContributorDisplayGroupName = "Vorschläge");

		AssertElementNotPresent(
			key_searchAssetDescription = "description",
			key_searchAssetTitle = "Titel",
			key_suggestionContributorDisplayGroupName = "Vorschläge",
			locator1 = "SearchBarSuggestions#RESULTS_LIST_ITEM_DESCRIPTION");
	}

	@description = "This is a use case for LPS-162509."
	@priority = 4
	test ViewSuggestionsLocalizedDisplayGroupName {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Language Override");

		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "TextInput#KEY",
			value1 = "top-search-keywords");

		Type(
			key_text = "en-US",
			locator1 = "TextInput#ANY",
			value1 = "Top Search Keywords");

		PortletEntry.save();

		Blueprints.openBlueprintsAdmin();

		Blueprints.createBlueprint(title = "Test Blueprint");

		Navigator.openURL();

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(suggestionContributorSize = 3);

		SearchBarSuggestions.configureEmbeddedSearchBarSuggestions(
			addContributor = "true",
			suggestionContributor = "Blueprint",
			suggestionContributorBlueprint = "Test Blueprint",
			suggestionContributorDisplayGroupName = "top-search-keywords",
			suggestionContributorOrder = 2,
			suggestionContributorSize = 3);

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "test");

		AssertTextEquals.assertPartialText(
			key_suggestionContributorDisplayGroupName = "Top Search Keywords",
			locator1 = "SearchBarSuggestions#RESULTS_LIST_SUGGESTION_CONTRIBUTOR_DISPLAY_GROUP_NAME",
			value1 = "TOP SEARCH KEYWORDS");
	}

}