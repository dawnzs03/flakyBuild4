apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)
task installGradleTestSmokeSample100(type: GradleBuild)

String gradleVersion = "7.3.3"
File userMavenRepoLocalDir = new File(System.getProperty("user.home"), ".m2/repository")

tasks.eclipse {
	ext.gradleVersion = gradleVersion
}

copyGradleTestDependencies {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE

	from configurations.compileClasspath
	into jar.destinationDir
}

dependencies {
	api group: "com.liferay", name: "com.liferay.ant.bnd", version: "3.2.10"
	api group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.48"

	compileOnly fileTree(builtBy: [rootProject.tasks.getByName("extractGradleApi" + gradleVersion.replace(".", ""))], dir: new File(rootProject.buildDir, "gradle-${gradleVersion}"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn installGradleTestSmokeSample100
	dependsOn jar

	deprecationMessagesAreFailures = false

	versions gradleVersion
}

installGradleTestSmokeSample100 {
	dir = "src/gradleTest/smoke/dependencies/sample-100"
	startParameter.systemPropertiesArgs = ["maven.repo.local": userMavenRepoLocalDir.absolutePath]
	tasks = ["publishToMavenLocal"]
}

pluginBundle {
	tags = ["baseline", "bnd", "bundle", "java", "osgi", "semantic", "semver", "versioning"]
}