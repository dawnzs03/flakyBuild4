/**
 * SPDX-FileCopyrightText: (c) 2000 Liferay, Inc. https://liferay.com
 * SPDX-License-Identifier: LGPL-2.1-or-later OR LicenseRef-Liferay-DXP-EULA-2.0.0-2023-06
 */

publishing {
	publications {
		maven(MavenPublication) {
			pom {
				description = project.description

				developers {
					developer {
						name = project.properties["pom.developer.name"] ?: "Brian Wing Shun Chan"
						organization = project.properties["pom.developer.organization"] ?: "Liferay, Inc."
						organizationUrl = project.properties["pom.developer.organizationUrl"] ?: "http://www.liferay.com"
					}
				}

				licenses {
					license {
						name = project.properties["pom.license.name"] ?: "LGPL 2.1"
						url = project.properties["pom.license.url"] ?: "http://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
					}
				}

				scm {
					connection = project.properties["pom.scm.connection"] ?: "scm:git:git@github.com:liferay/liferay-portal.git"
					developerConnection = project.properties["pom.scm.developerConnection"] ?: "scm:git:git@github.com:liferay/liferay-portal.git"
					url = project.properties["pom.scm.url"] ?: "http://github.com/liferay/liferay-portal"
				}
			}
		}
	}

	repositories {
		maven {
			credentials {
				password = project.properties["sonatype.release.password"]
				username = project.properties["sonatype.release.username"]
			}

			name = "releases"

			String repositoryURL = project.properties["sonatype.release.url"] ?: "https://repository-cdn.liferay.com/nexus/content/repositories/liferay-public-releases"

			if (project.path.startsWith(":dxp:") && !project.path.endsWith("-api") && !project.path.endsWith("-spi") && !project.path.contains("-demo-") && !project.path.contains("-demo-data-") && (repositoryURL.equals("https://repository-cdn.liferay.com/nexus/content/repositories/liferay-public-releases") || repositoryURL.equals("https://repository-cdn.liferay.com/nexus/content/repositories/liferay-public-releases"))) {
				repositoryURL = "https://repository-cdn.liferay.com/nexus/content/repositories/liferay-private-releases"
			}

			url = repositoryURL
		}
	}
}