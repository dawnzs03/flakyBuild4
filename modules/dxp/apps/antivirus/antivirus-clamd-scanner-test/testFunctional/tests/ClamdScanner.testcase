@component-name = "antivirus"
definition {

	property custom.properties = "dl.store.antivirus.enabled=true";
	property osgi.module.configuration.file.names = "com.liferay.antivirus.clamd.scanner.internal.configuration.ClamdAntivirusScannerConfiguration.config";
	property osgi.module.configurations = "hostname=\"clamd\"${line.separator}port=I\"3310\"";
	property portal.release = "true";
	property portal.upstream = "true";
	property test.liferay.virtual.instance = "false";
	property testray.main.component.name = "Antivirus";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();
		}
	}

	@description = "Verify normal documents can be added when Clamd Antivirus is enabled."
	@priority = 5
	test CanAddDocumentWhenEnablingAntivirus {
		task ("When the user tries uploading a normal document") {
			DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

			DMDocument.addCP(
				dmDocumentFile = "Document_1.docx",
				dmDocumentTitle = "Document_1.docx");
		}

		task ("Then the document is uploaded without issue") {
			Alert.viewSuccessMessage();

			AssertElementNotPresent(locator1 = "Message#ERROR");

			DMNavigator.openToEntryInAdmin(
				dmDocumentTitle = "Document_1.docx",
				groupName = "Guest",
				siteURLKey = "guest");

			DMDocument.viewCP(dmDocumentTitle = "Document_1.docx");
		}
	}

	@description = "Verify Clamd Antivirus will block a document that contains a virus uploaded to Blogs."
	@priority = 5
	test CannotUploadVirusToBlogs {
		BlogsNavigator.openBlogsAdmin(siteURLKey = "guest");

		Blogs.addEntryWithUploadedCoverImage(
			coverImageName = "eicar.jpg",
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title",
			invalidFileValidation = "A virus was detected in the file.",
			navTab = "Documents and Media",
			uploadFileName = "eicar.jpg");

		Button.clickPublish();

		BlogsNavigator.openToEditEntryInSite(
			entryTitle = "Blogs Entry Title",
			groupName = "Guest",
			siteURLKey = "guest");

		AssertElementNotPresent(
			key_coverImageName = "eicar.jpg",
			locator1 = "BlogsEntry#ENTRY_COVER_IMAGE_UNPUBLISHED");
	}

	@description = "Verify Clamd Antivirus will block a document that contains a virus uploaded to a depot."
	@priority = 5
	test CannotUploadVirusToDepot {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");

		DepotNavigator.openDepotDocumentsAndMediaAdmin(depotName = "Test Depot Name");

		DMDocument.addCP(
			dmDocumentFile = "eicar.com",
			dmDocumentTitle = "DM Document Title");

		Alert.viewErrorMessage(errorMessage = "A virus was detected in the file.");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_2",
			value1 = "Please select the file again.");
	}

	@description = "Verify Clamd Antivirus will block a document that contains a virus uploaded to an image fragment."
	@priority = 5
	test CannotUploadVirusToFragment {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page Name",
			type = "content");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Page Name",
			siteName = "Guest");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Image");

		PageEditor.editFragmentImage(
			fragmentName = "Image",
			id = "image-square",
			imageFileName = "eicar.jpg",
			invalidFileValidation = "A virus was detected in the file.",
			uploadFileName = "eicar.jpg");

		AssertElementNotPresent(
			fragmentName = "image",
			id = "image-square",
			image = "eicar.jpg",
			index = 1,
			locator1 = "Fragment#CONTRIBUTED_FRAGMENT_IMAGE");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Page Name",
			siteName = "Guest");

		AssertElementNotPresent(
			fragmentName = "image",
			id = "image-square",
			image = "eicar",
			index = 1,
			locator1 = "Fragment#CONTRIBUTED_FRAGMENT_IMAGE");
	}

	@description = "Verify Clamd Antivirus will block a document that contains a virus uploaded to Web Content."
	@priority = 5
	test CannotUploadVirusToWebContent {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DEBuilder.addField(
			fieldLabel = "Upload",
			fieldName = "Upload");

		WebContentStructures.saveCP();

		WebContentNavigator.openWebContentTemplatesAdmin(siteURLKey = "guest");

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "Template Description",
			templateFieldNames = "Upload",
			templateName = "Template Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "guest");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			dmNavTab = "User Personal Folder",
			invalidFileValidation = "A virus was detected in the file.",
			webContentTitle = "Web Content Title",
			webContentUpload = "eicar.com");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		DERenderer.assertUploadDataIsEmpty();
	}

	@description = "LPS-157927 Verify Clamd Antivirus will block a document that contains a virus while adding."
	@priority = 5
	test CannotUploadVirusWhileAddingDocument {
		task ("When the user tries uploading a document that contains a virus") {
			DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

			DMDocument.addCP(
				dmDocumentFile = "eicar.com",
				dmDocumentTitle = "eicar.com");
		}

		task ("Then the document upload is blocked") {
			Alert.viewErrorMessage(errorMessage = "A virus was detected in the file.");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "Please select the file again.");
		}

		task ("When the user clicks Publish again after the blocked upload") {
			Button.clickPublish();
		}

		task ("Then an empty document of the same name is uploaded") {
			Alert.viewSuccessMessage();

			DMNavigator.openToEntryInAdmin(
				dmDocumentTitle = "eicar.com",
				groupName = "Guest",
				siteURLKey = "guest");

			AssertElementNotPresent(
				key_text = "Download",
				locator1 = "Button#ANY");
		}
	}

	@description = "Verify Clamd Antivirus will block a document that contains a virus uploaded to DM while editing."
	@priority = 5
	test CannotUploadVirusWhileEditingDocument {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		DMNavigator.openToEditEntryInSite(
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.editCmd(
			dmDocumentFileEdit = "eicar.com",
			dmDocumentTitle = "DM Document Title");

		Button.clickPublish();

		Alert.viewErrorMessage(errorMessage = "A virus was detected in the file.");

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_2",
			value1 = "Please select the file again.");

		Button.clickPublish();

		Alert.viewSuccessMessage();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.downloadCP(
			dmDocumentSize = "13 KB",
			dmDocumentTitle = "DM Document Title");

		DMDocument.assertFileNameFromTempFolder(fileName = "Document_1.jpg");
	}

}