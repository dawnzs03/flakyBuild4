@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "osb-site-initializer-customer-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Customer Portal";

	var testSiteName = "Customer Portal Site";
	var testSiteURL = "customer-portal-site";
	var testUserFirstName = "Test";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Customer Portal Site") {
			CustomerPortal.addCustomerPortalSite(siteName = ${testSiteName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		CustomerPortal.customerPortalTearDown();

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = ${testSiteName});
		}
	}

	@description = "Story ID: LPS-148368| Test Summary: Verify that clicking in the button 'Watch Tutorial Videos', the user is redirect to the correct URL"
	@priority = 2
	test AssertThatGuideButtonIsPresent {
		task ("Given that I go to the Customer Portal") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.waitCP20PageToLoad(userFirstName = ${testUserFirstName});
		}

		task ("Then I click on Watch Onboarding Videos button in the banner") {
			CP2HomePage.assertGuideVideosButton();
		}
	}

	@description = "Story ID: LPS-152144 | Test Summary: Verify that clicking the Cookie Policy link opens a new page"
	@priority = 3
	test AssertThatTheCookiePolicyPageOpens {
		task ("Given that I go to the Customer Portal") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.waitCP20PageToLoad(userFirstName = ${testUserFirstName});
		}

		task ("When I click on the Cookie police link at the bottom of the page") {
			CP2HomePage.clickAtCookiePoliceLink();
		}

		task ("Then I should be able to assert that the Cookie police page is loaded") {
			CustomerPortal.assertPageTitle(title = "Cookie List");
		}
	}

	@description = "Story ID: LPS-152144 | Test Summary: Verify if the user selects another language, the page is translated into the selected language"
	@priority = 4
	test AssertThatTheLanguageLinksWork {
		task ("Given that I go to the Customer Portal") {
			ApplicationsMenu.gotoSite(key_site = ${testSiteName});

			CustomerPortal.waitCP20PageToLoad(userFirstName = ${testUserFirstName});
		}

		task ("When I change the site language") {
			CP2HomePage.clickAtLanguageButton(language = "portuguÃªs-Brasil");
		}

		task ("Then I should be able to assert the selected language") {
			CP2HomePage.assertLanguageChange(siteTitle = "Bem vindo");
		}
	}

}