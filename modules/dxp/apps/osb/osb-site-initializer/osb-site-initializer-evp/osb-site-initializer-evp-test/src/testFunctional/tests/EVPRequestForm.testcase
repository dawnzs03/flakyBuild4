@component-name = "OSB Site Initializer EVP"
definition {

	var baseURL = PropsUtil.get("portal.url");

	property custom.properties = "feature.flag.LPS-169981=true";
	property osgi.modules.includes = "osb-site-initializer-evp";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer EVP";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new EVP Site") {
			EVPUtils.addEVPSite();
		}

		task ("Log as Employee user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "employee@liferay.com",
				userLoginFullName = "Employee User");
		}

		task ("Navigate to Create a Request") {
			EVPUtils.gotoEVP();

			EVPOrganization.goToCreateOrganization();

			EVPOrganization.fillOrganizationForm(
				bankingInfo = "Bank Test",
				city = "City Test",
				contactEmail = "test@liferay.com",
				contactName = "Contact Test",
				contactPhone = 12345,
				country = "Country Test",
				createRequest = "false",
				organizationName = "Organization Test",
				organizationSite = "www.test.com.br",
				smallDescription = "Description Test",
				state = "State Test",
				street = "Street Test",
				taxId = 12345,
				zipCode = "12.345-67");

			EVPRequest.goToCreateRequest();
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		if (${testPortalInstance} == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "EVP Portal");
		}
	}

	@description = "LPS-162902 - Verify that can cancel the request creation"
	@priority = 3
	test CanCancelRequestCreation {
		task ("When the user clicks on the Cancel button on the request form") {
			Button.clickCancel();
		}

		task ("Then the user can see that there were no requests being showed") {
			EVPUtils.viewNotPresentStatus(
				organizationName = "Organization Test",
				status = "Awaiting Approval On EVP");
		}
	}

	@description = "LPS-162902 - Verify that can create a Request by clicking on 'Create a Request'"
	@priority = 4
	test CanCreateRequestForm {
		task ("When The employee user creates a request") {
			EVPRequest.fillRequestForm(
				grantAmount = 10,
				grantRequestType = "Proposed",
				organization = "Organization Test",
				phoneNumber = 12345,
				requestBehalf = "Global/Local Poor",
				requestDescription = "Request Description Test",
				requestPurposes = "Basic Education",
				requestType = "Grant");
		}

		task ("Then the user can see the created request on listing") {
			EVPNavigator.goToNavItem(navItem = "Request");

			EVPUtils.viewStatus(
				organizationName = "Organization Test",
				status = "Awaiting Approval On EVP");
		}
	}

	@description = "LPS-162902 - Verify that can not submit the request form without fill all required fields"
	@priority = 3
	test CannotSubmitFormWithoutRequiredFields {
		task ("When the user clicks on submit button in the request form without filling out any required fields") {
			Button.click(button = "Submit");
		}

		task ("Then the required field message appears") {
			AssertAttributeValue(
				attribute1 = "required",
				key_text = "Phone Number",
				locator1 = "TextInput#ANY",
				value1 = "true");
		}
	}

}