definition {

	macro addProductVersionInBuild {
		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "Testray#FIELD_NAME_MODAL",
			value1 = ${key_name});

		Click(locator1 = "Modal#SAVE_BUTTON");
	}

	macro changeTeamToComponent {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		Select(
			key_fieldLabel = "Team",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = ${newTeamName});

		Button.clickSave();
	}

	macro clickOptionSidebar {
		Click(
			key_button = ${optionSidebar},
			locator1 = "TestrayDropdown#SIDEBAR_CONTENT");
	}

	macro createCaseTypes {
		Click(locator1 = "Testray#MODAL_PLUS");

		if (isSet(caseTypeName)) {
			PortletEntry.inputName(name = ${caseTypeName});
		}

		Button.clickSave();
	}

	macro createComponent {
		SelectFrame(locator1 = "Testray#MODAL_BODY");

		Click(locator1 = "Testray#MODAL_PLUS");

		if (isSet(componentName)) {
			PortletEntry.inputName(name = ${componentName});
		}

		if (isSet(teamName)) {
			Select(
				key_fieldLabel = "Team",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${teamName});
		}

		Button.clickSave();
	}

	macro createNewCategory {
		Click(locator1 = "Testray#MODAL_PLUS");

		if (isSet(category)) {
			PortletEntry.inputName(name = ${category});
		}

		Button.clickSave();
	}

	macro createNewOption {
		Click(locator1 = "Testray#MODAL_PLUS");

		PortletEntry.inputName(name = ${option});

		if (isSet(category)) {
			Select(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "factorCategoryId",
				value1 = ${category});
		}

		Button.clickSave();
	}

	macro createProductVersion {
		Click(locator1 = "Testray#MODAL_PLUS");

		if (isSet(productVersion)) {
			PortletEntry.inputName(name = ${productVersion});
		}

		Button.clickSave();
	}

	macro createTeam {
		Click(locator1 = "Testray#MODAL_PLUS");

		if (isSet(teamName)) {
			PortletEntry.inputName(name = ${teamName});
		}

		if (isSet(component)) {
			Click(
				key_parameterType = "Unassigned",
				locator1 = "Testray#PARAMETER_TYPE_OPTIONS",
				value1 = ${component});

			Click(
				key_arrowDirection = "Right",
				key_parameterType = "Unassigned",
				locator1 = "Testray#ARROW_DIRECTION");
		}

		Button.clickSave();
	}

	macro deleteAllCasesTypes {
		Refresh();

		TestrayDropdown.selectOptionSidebar(
			buttonOption = "Manage",
			menuItem = "Case Types");

		Testray.changePagination(
			modal = "true",
			quantity = 150);

		while (IsElementPresent(key_tableEntry = "Case_", locator1 = "Testray#TABLE_ENTRY")) {
			TestrayManage.selectTableRowOption(
				rowOption = "Delete",
				tableEntry = "Case_");

			AssertConfirm(value1 = "Are you sure you want to delete this item?");

			Alert.viewSuccessMessage();

			Testray.checkSuccessAlertAppearsAndDisappears();
		}
	}

	macro deleteAllCategories {
		Refresh();

		TestrayDropdown.selectOptionSidebar(
			buttonOption = "Manage",
			menuItem = "Categories");

		while (IsElementPresent(key_tableEntry = "Environments_", locator1 = "Testray#TABLE_ENTRY")) {
			TestrayManage.selectTableRowOption(
				rowOption = "Delete",
				tableEntry = "Environments_");

			AssertConfirm(value1 = "Are you sure you want to delete this item?");

			Alert.viewSuccessMessage();

			Testray.checkSuccessAlertAppearsAndDisappears();
		}
	}

	macro deleteCasesTypes {
		TestrayManage.selectTableRowOption(
			rowOption = "Delete",
			tableEntry = ${tableEntry});

		AssertConfirm(value1 = "Are you sure you want to delete this item?");

		Alert.viewSuccessMessage();

		Testray.checkSuccessAlertAppearsAndDisappears();
	}

	macro deleteCategory {
		TestrayDropdown.selectOptionSidebar(
			buttonOption = "Manage",
			menuItem = "Categories");

		TestrayManage.selectTableRowOption(
			rowOption = "Delete",
			tableEntry = ${categoryName});

		AssertConfirm(value1 = "Are you sure you want to delete this item?");

		Alert.viewSuccessMessage();

		Testray.checkSuccessAlertAppearsAndDisappears();
	}

	macro editCaseType {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		if (isSet(newName)) {
			PortletEntry.inputName(name = ${newName});
		}

		Button.clickSave();

		Testray.checkSuccessAlertAppearsAndDisappears();
	}

	macro editCategory {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		PortletEntry.inputName(name = ${newName});

		Button.clickSave();
	}

	macro editComponent {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		if (isSet(newComponentName)) {
			PortletEntry.inputName(name = ${newComponentName});
		}

		if (isSet(newTeamName)) {
			Select(
				key_fieldLabel = "Team",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = ${newTeamName});
		}

		Button.clickSave();
	}

	macro editOption {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		PortletEntry.inputName(name = ${newName});

		if (isSet(category)) {
			Select(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "factorCategoryId",
				value1 = ${category});
		}

		Button.clickSave();

		Testray.checkSuccessAlertAppearsAndDisappears();
	}

	macro editProductVersion {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		PortletEntry.inputName(name = ${newName});

		Button.clickSave();
	}

	macro editTeam {
		TestrayManage.selectTableRowOption(
			rowOption = "Edit",
			tableEntry = ${tableEntry});

		if (isSet(newName)) {
			PortletEntry.inputName(name = ${newName});
		}

		Button.clickSave();
	}

	macro selectTableRowOption {
		WaitForElementPresent(
			key_tableEntry = ${tableEntry},
			locator1 = "Testray#TABLE_ENTRY");

		RightClick.rightClick(
			key_tableEntry = ${tableEntry},
			locator1 = "Testray#TABLE_ENTRY");

		MenuItem.clickNoError(menuItem = ${rowOption});
	}

}