@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var caseType = "Case Type 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete Testray Site and Client Extensions") {
			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-146938 | Test Summary: Verify if the Administrator can Add Case Types"
	@priority = 5
	test AdminCanAddCaseTypes {
		task ("Given that I go to the Case Type page") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Case Types");
		}

		task ("When I create a Case Type") {
			TestrayManage.createCaseTypes(caseTypeName = ${caseType});
		}

		task ("Then assert that the newly created case type is present in the list") {
			Testray.viewTableEntryList(listEntry = ${caseType});
		}

		task ("Delete the case type via API") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = ${caseType});
		}
	}

	@description = "Story ID: LPS-146938 | Test Summary: Verify that as an Administrator I can delete Case Types"
	@priority = 5
	test AdminCanDeleteCaseTypes {
		task ("Given that I create a case type via API") {
			JSONTestray.addCaseType(caseType = ${caseType});
		}

		task ("When I go to the case types") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Case Types");

			Testray.changePagination(
				modal = "true",
				quantity = 150);
		}

		task ("When I delete a Case Type") {
			TestrayManage.deleteCasesTypes(tableEntry = ${caseType});
		}

		task ("Then I shouldn't be able to see the same in the list") {
			Testray.viewTableEntryListNotPresent(listEntry = ${caseType});
		}
	}

	@description = "Story ID: LPS-146938 | Test Summary: Verify if the Administrator can Edit Case Types"
	@priority = 5
	test AdminCanEditCaseTypes {
		task ("Given that I create a Case Type via API") {
			JSONTestray.addCaseType(caseType = ${caseType});
		}

		task ("When I go to the case types") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Case Types");

			Testray.changePagination(
				modal = "true",
				quantity = 150);
		}

		task ("And I edit the name of the Case Type") {
			TestrayManage.editCaseType(
				newName = "Case Type Edited",
				tableEntry = ${caseType});
		}

		task ("Then I assert that the name has been changed in the list") {
			Testray.viewTableEntryList(listEntry = "Case Type Edited");
		}

		task ("Delete the case type via API") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = "Case Type Edited");
		}
	}

	@description = "Story ID: LPS-165582 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate cases types"
	@priority = 3
	test AdminCanNotAddDuplicateCasesTypes {
		task ("Given that I create a Case Type via API") {
			JSONTestray.addCaseType(caseType = ${caseType});
		}

		task ("When I go to the case types") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Case Types");

			Testray.changePagination(
				modal = "true",
				quantity = 150);
		}

		task ("When I try to create a new Case Type with an existing title") {
			TestrayManage.createCaseTypes(caseTypeName = ${caseType});
		}

		task ("Then an error message is shown") {
			Alert.viewErrorMessage(errorMessage = "The Case Type name already exists");
		}
	}

	@description = "Story ID: LPS-146938 | Test Summary: Verify that the user can not create a case type if all the required fields are not filled in"
	@priority = 2
	test AdminCanViewRequiredField {
		task ("Given that go to the case types") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			TestrayDropdown.selectOptionSidebar(
				buttonOption = "Manage",
				menuItem = "Case Types");

			Testray.changePagination(
				modal = "true",
				quantity = 150);
		}

		task ("When create a case type without the field fill") {
			TestrayManage.createCaseTypes();
		}

		task ("Then can see the message of required field") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

}