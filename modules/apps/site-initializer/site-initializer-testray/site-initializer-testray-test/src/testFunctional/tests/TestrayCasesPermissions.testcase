@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var caseName = "Case 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var projectName = "Project 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("create project, team, component, case type and case via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				description = "Description 1",
				priority = 5,
				projectName = ${projectName},
				steps = "Steps 1");
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Logout And Login with another user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "test test");
		}

		task ("Delete case type and project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = ${caseType});

			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-178255 | Test Summary: Verify that the User with the 'Testray Analyst' role should be able to create/edit/delete the case"
	@priority = 4
	test AssertAnalystRoleCanCreateEditAndDeleteCase {
		task ("Given that the user login with the Testray Analyst role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "analyst@testray.com",
				userLoginFullName = "Analyst User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("And the user go to the Cases page") {
			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Cases");
		}

		task ("And the user create a case") {
			TestrayCases.createCases(
				caseName = "Case 2",
				caseType = ${caseType},
				component = ${componentName},
				description = "description case",
				duration = 60,
				priority = 3,
				steps = "Auto Test Steps");
		}

		task ("And the user can see the case in the table") {
			Testray.viewTableEntryList(listEntry = "Case 2");
		}

		task ("And the user edit a case") {
			TestrayManage.selectTableRowOption(
				rowOption = "Edit",
				tableEntry = "Case 2");

			TestrayCases.editCases(
				caseType = "Case Type 1",
				component = "Component 1",
				description = "Case Description",
				duration = 30,
				newName = "Case Edited",
				priority = 3,
				steps = "http://test.edit.com");
		}

		task ("And the user can see the case in the table") {
			Testray.viewTableEntryList(listEntry = "Case Edited");
		}

		task ("When the user delete a case") {
			Testray.deleteItem(itemName = "Case Edited");
		}

		task ("Then the case is no longer present in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Case Edited");
		}
	}

	@description = "Story ID: LPS-178255 | Test Summary: Verify that the User with the 'Testray Lead' role should be able to create/edit/delete the case"
	@priority = 4
	test AssertLeadRoleCanCreateEditAndDeleteCase {
		task ("Given that the user login with the Testray Lead role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "lead@testray.com",
				userLoginFullName = "Lead User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("And the user go to the Cases page") {
			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Cases");
		}

		task ("And the user create a case") {
			TestrayCases.createCases(
				caseName = "Case 2",
				caseType = ${caseType},
				component = ${componentName},
				description = "description case",
				duration = 60,
				priority = 3,
				steps = "Auto Test Steps");
		}

		task ("And the user can see the case in the table") {
			Testray.viewTableEntryList(listEntry = "Case 2");
		}

		task ("And the user edit a case") {
			TestrayManage.selectTableRowOption(
				rowOption = "Edit",
				tableEntry = "Case 2");

			TestrayCases.editCases(
				caseType = "Case Type 1",
				component = "Component 1",
				description = "Case Description",
				duration = 30,
				newName = "Case Edited",
				priority = 3,
				steps = "http://test.edit.com");
		}

		task ("And the user can see the case in the table") {
			Testray.viewTableEntryList(listEntry = "Case Edited");
		}

		task ("When the user delete a case") {
			Testray.deleteItem(itemName = "Case Edited");
		}

		task ("Then the case is no longer present in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Case Edited");
		}
	}

	@description = "Story ID: LPS-178255 | Test Summary: Verify that the User with the 'Testray User' role should not be able to create/edit/delete the case but can view"
	@priority = 2
	test AssertUserRoleCanNotCreateEditAndDeleteCase {
		task ("Given that the user login with the Testray User role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "user@testray.com",
				userLoginFullName = "User User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("And the user go to the Cases page") {
			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Cases");
		}

		task ("And can see the case in the table") {
			Testray.viewTableEntryList(listEntry = ${caseName});
		}

		task ("And right click in case") {
			AssertElementNotPresent(locator1 = "Button#PLUS");

			RightClick.rightClick(
				key_tableEntry = ${caseName},
				locator1 = "Testray#TABLE_ENTRY");
		}

		task ("And can not see the Edit and Delete options in right click") {
			MenuItem.viewNotPresent(menuItem = "Edit");

			MenuItem.viewNotPresent(menuItem = "Delete");
		}

		task ("When click kebab in Case") {
			Testray.clickTableEntry(tableEntry = ${caseName});

			Click(locator1 = "ObjectAdmin#KEBAB_MENU");
		}

		task ("Then can not see the Edit and Delete options in the kebab") {
			AssertElementNotPresent(
				key_kebabOption = "Edit",
				locator1 = "ObjectAdmin#KEBAB_MENU_OPTION");

			AssertElementNotPresent(
				key_kebabOption = "Delete",
				locator1 = "ObjectAdmin#KEBAB_MENU_OPTION");
		}
	}

}