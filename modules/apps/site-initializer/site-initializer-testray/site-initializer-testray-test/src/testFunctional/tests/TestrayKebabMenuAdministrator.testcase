@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseResultName = "case result 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var requirementName = "Requirement Summary";
	var routineName = "Routine 1";
	var runName = "Option 1";
	var suiteName = "Suite 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Create project, team, component, routine, case type, case, environment factor, product version, build, run, case result via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addProject(
				projectDescription = "Description",
				projectName = "Project 2");

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "The front page of the internet",
				projectName = ${projectName},
				requirementName = ${requirementName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = ${buildName},
				productVersion = ${productVersion},
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addRun(
				buildName = ${buildName},
				runName = ${runName});

			JSONTestray.addCaseResult(
				buildName = ${buildName},
				caseName = ${caseName},
				caseResultName = ${caseResultName},
				componentName = ${componentName},
				runName = ${runName});
		}

		task ("Go to Testray 2 and change the pagination") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete case type, factor category, routine and project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = ${caseType});

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "factorcategories",
				objectName = "Category 1");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "routines",
				objectName = ${routineName});

			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			JSONTestrayAPI.deleteProject(projectName = "Project 2");

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can add Manage Components in the kebab menu"
	@priority = 3
	test AdminCanAddManageComponents {
		task ("Given that the user go to components") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.kebabMenuOption(kebabOption = "Manage Components");
		}

		task ("When the user create the new component through kebab") {
			TestrayManage.createComponent(componentName = "Component 2");
		}

		task ("Then the user can see the new component in the table") {
			Testray.viewTableEntryList(listEntry = "Component 2");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can add Manage Product Versions in the kebab menu"
	@priority = 3
	test AdminCanAddManageProductVersions {
		task ("Given that the user go to product version") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.kebabMenuOption(kebabOption = "Manage Product Versions");
		}

		task ("When the user create the new product version through kebab") {
			TestrayManage.createProductVersion(productVersion = "7.4");
		}

		task ("Then the user can see the new product version in the table") {
			Testray.viewTableEntryList(listEntry = "7.4");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can add Manage Teams in the kebab menu"
	@priority = 3
	test AdminCanAddManageTeams {
		task ("Given that the user go to teams") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.kebabMenuOption(kebabOption = "Manage Teams");
		}

		task ("When the user create the new teams through kebab") {
			TestrayManage.createTeam(teamName = "Team 2");
		}

		task ("Then the user can see the new teams in the table") {
			Testray.viewTableEntryList(listEntry = "Team 2");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can delete cases in the kebab menu"
	@priority = 3
	test AdminCanDeleteCases {
		task ("Given that go to the case created") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Cases");

			Testray.clickTableEntry(tableEntry = ${caseName});
		}

		task ("When delete the routine through kebab") {
			Testray.deleteByKebab(kebabOption = "Delete Case");
		}

		task ("Then can not see the case in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = ${caseName});
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can delete project in the kebab menu"
	@priority = 3
	test AdminCanDeleteProject {
		task ("Given that go to the project created") {
			Testray.clickTableEntry(tableEntry = "Project 2");
		}

		task ("When delete the project through kebab") {
			Testray.deleteByKebab(kebabOption = "Delete Project");
		}

		task ("And the user is redirected to the project page") {
			WaitForElementPresent(
				key_pageTabTitle = "Projects",
				locator1 = "Testray#PAGE_TAB_TITLE");

			Testray.assertTabPageTitle(pageTabTitle = "Projects");

			Testray.changePagination(quantity = 150);
		}

		task ("Then can not see the project in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Project 2");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if As an Administrator, I should be able to delete Requirements in the kebab menu"
	@priority = 3
	test AdminCanDeleteRequirements {
		task ("Given that go to the requirement created") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Requirements");

			Testray.clickTableEntry(tableEntry = ${requirementName});
		}

		task ("When delete the requirement through kebab") {
			Testray.deleteByKebab(kebabOption = "Delete Requirement");
		}

		task ("And the user is redirected to the requirement tab") {
			WaitForElementPresent(
				key_pageTabTitle = "Requirements",
				locator1 = "Testray#PAGE_TAB_TITLE");

			Testray.assertTabPageTitle(pageTabTitle = "Requirements");
		}

		task ("Then can not see the requirement in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = ${requirementName});
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can delete routine in the kebab menu"
	@priority = 3
	test AdminCanDeleteRoutine {
		task ("Given that go to the routine created") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.clickTableEntry(tableEntry = ${routineName});
		}

		task ("When delete the routine through kebab") {
			Testray.deleteByKebab(kebabOption = "Delete Routine");
		}

		task ("And the user is redirected to the routine page") {
			Testray.assertTabPageTitle(pageTabTitle = "Routines");
		}

		task ("Then can not see the routine in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = ${routineName});
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can edit build in the kebab menu"
	@priority = 3
	test AdminCanEditBuild {
		task ("Given that the user go to build") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.clickTableEntry(tableEntry = ${routineName});

			Testray.clickTableEntry(tableEntry = ${buildName});
		}

		task ("When the user edit the build through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Build");

			TestrayBuild.editBuild(
				caseList = ${caseName},
				description = "Description Edited",
				gitHash = 12345,
				newName = "Build Edited",
				productVersion = "1.2",
				routine = "Routine 1");
		}

		task ("Then the user is redirected to the build page and can see the edited build title") {
			Testray.assertTabPageTitle(pageTabTitle = "Tests");

			TestrayDropdown.assertBreadcrumbTitle(text = "Build Edited");
		}

		task ("Delete the build edited") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "builds",
				objectName = "Build Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can edit cases in the kebab menu"
	@priority = 3
	test AdminCanEditCases {
		task ("Given that the user go to case") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			PRMNavigator.goToNavItem(navItem = "Cases");

			Testray.clickTableEntry(tableEntry = ${caseName});
		}

		task ("When the user edit the case through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Case");

			TestrayCases.editCases(
				caseType = "Case Type 1",
				component = "Component 1",
				description = "Case Description",
				duration = 30,
				newName = "Case Edited",
				priority = 3,
				steps = "http://test.edit.com");
		}

		task ("Then the user is redirected to the case page and can see the edited case title") {
			Testray.assertTabPageTitle(pageTabTitle = "Test History");

			TestrayDropdown.assertBreadcrumbTitle(text = "Case Edited");
		}

		task ("Delete the case edited") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "cases",
				objectName = "Case Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can edit project in the kebab menu"
	@priority = 3
	test AdminCanEditProject {
		task ("Given that the user go to project") {
			Testray.clickTableEntry(tableEntry = ${projectName});
		}

		task ("When the user edit the project through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Project");

			TestrayProject.editProject(
				newDescription = "Project Description Edited",
				newName = "Project Edited");
		}

		task ("Then the user is redirected to the project page and can see the edited project title") {
			Testray.assertTabPageTitle(pageTabTitle = "Routines");

			TestrayDropdown.assertBreadcrumbTitle(text = "Project Edited");
		}

		task ("Delete the project edited") {
			JSONTestrayAPI.deleteProject(projectName = "Project Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if As an Administrator, I should be able to edit Requirements in the kebab menu"
	@priority = 3
	test AdminCanEditRequirements {
		task ("Given that the user go to requirement") {
			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "The front page of the internet",
				projectName = ${projectName},
				requirementName = "Requirement Summary");

			Testray.clickTableEntry(tableEntry = ${projectName});

			PRMNavigator.goToNavItem(navItem = "Requirements");

			Testray.clickTableEntry(tableEntry = "Requirement Summary");
		}

		task ("When the user edit the requirement through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Requirement");

			TestrayRequirements.editRequirement(
				componentId = "Component 1",
				linkTitle = "The page edited",
				linkURL = "https://reddit.com",
				newName = "Requirement Edited");
		}

		task ("Then the user is redirected to the requirement page and can see the edited requirement title") {
			Testray.assertTabPageTitle(pageTabTitle = "Cases");

			TestrayDropdown.assertBreadcrumbTitle(text = "Requirement Edited");
		}

		task ("Delete the requirement edited") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "requirements",
				objectName = "Requirement Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can edit routine in the kebab menu"
	@priority = 3
	test AdminCanEditRoutine {
		task ("Given that the user go to routine") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.clickTableEntry(tableEntry = ${routineName});
		}

		task ("When the user edit the routine through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Routine");

			TestrayRoutines.editRoutine(newName = "Routine Edited");
		}

		task ("Then the user is redirected to the routine page and can see the edited routine title") {
			Testray.assertTabPageTitle(pageTabTitle = "Build History");

			TestrayDropdown.assertBreadcrumbTitle(text = "Routine Edited");
		}

		task ("Delete the routine edited") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "routines",
				objectName = "Routine Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if As an Administrator, I should be able to edit Suite in the kebab menu"
	@priority = 3
	test AdminCanEditSuite {
		task ("Given that the user go to suite") {
			JSONTestray.addSuite(suiteName = ${suiteName});

			Testray.clickTableEntry(tableEntry = ${projectName});

			PRMNavigator.goToNavItem(navItem = "Suites");

			Testray.clickTableEntry(tableEntry = ${suiteName});
		}

		task ("When the user edit the suite through kebab") {
			Testray.kebabMenuOption(kebabOption = "Edit Suite");

			TestraySuites.editSuite(
				newDescription = "Suite Description Edited",
				newName = "Suite Edited");
		}

		task ("Then the user is redirected to the suite page and can see the edited suite title") {
			TestrayDropdown.assertBreadcrumbTitle(text = "Suite Edited");
		}

		task ("Delete the suite edited") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "suites",
				objectName = "Suite Edited");
		}
	}

	@description = "Story ID: LPS-159526 | Test Summary: Verify if as an administrator can promoted build in the kebab menu"
	@priority = 3
	test AdminCanPromotedBuilds {
		task ("Given that the user go to build") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.clickTableEntry(tableEntry = ${routineName});

			Testray.clickTableEntry(tableEntry = ${buildName});
		}

		task ("When the user promoted the build through kebab") {
			Testray.kebabMenuOption(kebabOption = "Promote");
		}

		task ("Then the promoted build should appear on the list") {
			Testray.clickBreadcrumb(text = ${routineName});

			TestrayBuild.verifyPromotedBuild();
		}
	}

}