@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseResultName = "case result 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var routineName = "Routine 1";
	var runName = "Option 1";
	var taskName = "Task_1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Create project, team, component, routine, case type, case, product version, environment factor, build, run and case result via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = ${buildName},
				productVersion = ${productVersion},
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addBuild(
				buildName = "Build 2",
				productVersion = ${productVersion},
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addRun(
				buildName = ${buildName},
				runName = ${runName});

			JSONTestray.addCaseResult(
				buildName = ${buildName},
				caseName = ${caseName},
				caseResultName = ${caseResultName},
				componentName = ${componentName},
				runName = ${runName});

			JSONTestray.addTask(
				buildName = ${buildName},
				key = "INANALYSIS",
				statusTask = "In Analysis",
				taskName = ${taskName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Logout And Login with test user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "test test");
		}

		task ("Delete task, routine, case type, factor category and project, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "tasks",
				objectName = "Task_1");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "routines",
				objectName = ${routineName});

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = ${caseType});

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "factorcategories",
				objectName = "Category 1");

			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Analyst' role should be able to edit the task"
	@priority = 4
	test AssertAnalystRoleCanEditTask {
		task ("Given that the user login with the Testray Analyst role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "analyst@testray.com",
				userLoginFullName = "Analyst User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();

			Testray.clickTableEntry(tableEntry = "Task_1");
		}

		task ("When the user edit a task") {
			TestrayTestflow.editTask(
				taskName = "Task_2",
				userNameList = "Analyst User");

			TestrayTestflow.goToTestflow();
		}

		task ("Then the user can see the task edited") {
			Testray.viewTableEntryList(listEntry = "Task_2");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Analyst' role should not be able to create the task"
	@priority = 3
	test AssertAnalystRoleCanNotCreateTask {
		task ("Given that the user login with the Testray Analyst role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "analyst@testray.com",
				userLoginFullName = "Analyst User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("When the user go to the TestFlow") {
			TestrayTestflow.goToTestflow();
		}

		task ("Then the user can not create a task") {
			AssertElementNotPresent(locator1 = "Button#PLUS");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Analyst' role should not be able to delete the task"
	@priority = 3
	test AssertAnalystRoleCanNotDeleteTask {
		task ("Given that the user login with the Testray Analyst role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "analyst@testray.com",
				userLoginFullName = "Analyst User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();

			Testray.clickTableEntry(tableEntry = "Task_1");
		}

		task ("When the user complete a task") {
			Button.click(button = "Complete");
		}

		task ("Then the user can not delete task") {
			Button.viewNotPresent(button = "Delete");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Lead' role should be able to create the task"
	@priority = 4
	test AssertLeadRoleCanCreateTask {
		task ("Given that the user login with the Testray Lead role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "lead@testray.com",
				userLoginFullName = "Lead User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();
		}

		task ("When the user created a task") {
			TestrayTestflow.addTask(
				caseTypeList = "Compile",
				selectBuild = "Build 2",
				selectProject = ${projectName},
				selectRoutine = ${routineName},
				taskName = "Task_2",
				userGroups = "false",
				userList = "Test Test");
		}

		task ("Then the user can see the Task created") {
			TestrayTestflow.goToTestflow();

			TestrayTestflow.assertTaskDetails(
				build = "Build 2",
				project = ${projectName},
				routine = ${routineName},
				status = "In Analysis",
				taskName = "Task_2");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Lead' role should be able to delete the task"
	@priority = 4
	test AssertLeadRoleCanDeleteTask {
		task ("Given that the user login with the Testray Lead role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "lead@testray.com",
				userLoginFullName = "Lead User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("And the user complete a task") {
			TestrayTestflow.goToTestflow();

			TestrayManage.selectTableRowOption(
				rowOption = "Complete",
				tableEntry = "Task_1");
		}

		task ("When the user delete a task") {
			Testray.deleteItem(itemName = "Task_1");
		}

		task ("Then the user can not see the task") {
			Testray.viewTableEntryListNotPresent(listEntry = "Task_1");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray Lead' role should be able to edit the task"
	@priority = 4
	test AssertLeadRoleCanEditTask {
		task ("Given that the user login with the Testray Lead role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "lead@testray.com",
				userLoginFullName = "Lead User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();

			Testray.clickTableEntry(tableEntry = "Task_1");
		}

		task ("When the user edit the task") {
			TestrayTestflow.editTask(
				taskName = "Task_2",
				userNameList = "Lead User");
		}

		task ("Then the user can see the Task edited") {
			TestrayTestflow.goToTestflow();

			TestrayTestflow.assertTaskDetails(
				build = ${buildName},
				project = ${projectName},
				routine = ${routineName},
				status = "In Analysis",
				taskName = "Task_2");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray User' role should not be able to create the task"
	@priority = 3
	test AssertUserRoleCanNotCreateTask {
		task ("Given that the user login with the Testray User role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "user@testray.com",
				userLoginFullName = "User User");

			Navigator.openSiteURL(siteName = "Testray 2");
		}

		task ("When the user go to the TestFlow") {
			TestrayTestflow.goToTestflow();
		}

		task ("Then the user can not create a task") {
			AssertElementNotPresent(locator1 = "Button#PLUS");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray User' role should not be able to delete the task"
	@priority = 3
	test AssertUserRoleCanNotDeleteTask {
		task ("Given that the user login with the Testray User role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "user@testray.com",
				userLoginFullName = "User User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();
		}

		task ("When the user go to the task") {
			Testray.clickTableEntry(tableEntry = "Task_1");
		}

		task ("Then the user can not delete task") {
			Button.viewNotPresent(button = "Delete");
		}
	}

	@description = "Story ID: LPS-178272 | Test Summary: Verify that the User with the 'Testray User' role should not be able to edit the task"
	@priority = 3
	test AssertUserRoleCanNotEditTask {
		task ("Given that the user login with the Testray User role") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "user@testray.com",
				userLoginFullName = "User User");

			Navigator.openSiteURL(siteName = "Testray 2");

			TestrayTestflow.goToTestflow();
		}

		task ("When the user go to the task") {
			Testray.clickTableEntry(tableEntry = "Task_1");
		}

		task ("Then the user can not edit a task") {
			Button.viewNotPresent(button = "Edit Task");
		}
	}

}