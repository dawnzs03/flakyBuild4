@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var productVersion = "1.2";
	var projectName = "Project 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Create project via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-159530 | Test Summary: Verify if As an Administrator I am able to add Product Versions"
	@priority = 5
	test AdminCanAddProductVersion {
		task ("Given that I go to Manage Product Version") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Product Versions",
				tableEntry = ${projectName});
		}

		task ("When I create a new Product Version via API") {
			TestrayManage.createProductVersion(productVersion = ${productVersion});
		}

		task ("Then I assert that the Product Version was created") {
			Testray.viewTableEntryList(listEntry = ${productVersion});
		}
	}

	@description = "Story ID: LPS-159530 | Test Summary: Verify if as an Administrator I am able to delete Product Versions"
	@priority = 5
	test AdminCanDeleteProductVersion {
		task ("Given that I create a new Product Version via API") {
			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});
		}

		task ("And I go to Manage Product Version") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Product Versions",
				tableEntry = ${projectName});
		}

		task ("When I delete the created version") {
			Testray.deleteItem(itemName = ${productVersion});
		}

		task ("Then I should be able to tell that the version has been deleted") {
			Testray.viewTableEntryListNotPresent(listEntry = ${productVersion});
		}
	}

	@description = "Story ID: LPS-159530 | Test Summary: Verify if as an Administrator I am able to Edit Product Versions"
	@priority = 5
	test AdminCanEditProductVersion {
		task ("Given that I create a new Product Version via API") {
			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});
		}

		task ("And I go to Manage Product Version") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Product Versions",
				tableEntry = ${projectName});
		}

		task ("When I edit the product version") {
			TestrayManage.editProductVersion(
				newName = "1.3.x",
				tableEntry = ${productVersion});
		}

		task ("Then I should be able the created product version") {
			Testray.viewTableEntryList(listEntry = "1.3.x");
		}
	}

	@description = "Story ID: LPS-159530 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate Product Versions"
	@priority = 3
	test AdminCanNotAddDuplicateProductVersion {
		task ("Given that I create a new Product Version via API") {
			JSONTestray.addProductVersion(
				productVersion = ${productVersion},
				projectName = ${projectName});
		}

		task ("When I go to Manage Product Version and I try to duplicate a version") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Product Versions",
				tableEntry = ${projectName});

			TestrayManage.createProductVersion(productVersion = ${productVersion});
		}

		task ("Then I assert that the error message appears") {
			Alert.viewErrorMessage(errorMessage = "The Product Version name already exists");
		}
	}

	@description = "Story ID: LPS-159530 | Test Summary: Verify if as an Administrator I can see the warning message 'This field is required' when trying to save without a name"
	@priority = 2
	test AdminCanViewWarningMessage {
		task ("Given I go to the Manage Product Versions page and try to create a version without filling in the 'Name' field") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Product Versions",
				tableEntry = ${projectName});

			TestrayManage.createProductVersion();
		}

		task ("Then I assert that the 'Name' field is required") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

}