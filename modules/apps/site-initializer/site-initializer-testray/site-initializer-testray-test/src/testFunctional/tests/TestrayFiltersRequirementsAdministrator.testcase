@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var componentName = "Component 1";
	var projectName = "Project 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("create project, team, component and requirement via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "The front page of the internet",
				projectName = ${projectName},
				requirementName = "Requirement 1");

			JSONTestray.addRequirement(
				componentName = ${componentName},
				linkTitle = "Page of the internet 2",
				linkURL = "https://reddit.com",
				projectName = ${projectName},
				requirementName = "Requirement 2");
		}

		task ("Go to the Requirements tab") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Navigator.gotoNavTab(navTab = "Requirements");
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should not see requirements that don't exist when I try filtering for them"
	@priority = 3
	test AdminCanFilterForNonexistentRequirements {
		task ("When the user filters a requirement by its non-existent name") {
			TestrayFilter.filterResults(
				fieldType = "input",
				inputFieldLabel = "Summary",
				inputValue = "Requirement 3");
		}

		task ("Then can see message no results found") {
			TestrayFilter.assertNoResultsFound();
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should be able to filter requirements by component"
	@priority = 3
	test AdminCanFilterRequirementsByComponent {
		task ("When the user filter a requirement by component") {
			TestrayFilter.filterResults(
				fieldType = "multiSelect",
				multiSelectFieldLabel = "Component",
				multiSelectValue = "Component 1");
		}

		task ("Then can see the component selected in the table") {
			Testray.viewTableEntryList(listEntry = "Component 1");
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should be able to filter requirements by link"
	@priority = 3
	test AdminCanFilterRequirementsByLink {
		task ("When the user filter a requirement by link") {
			TestrayFilter.filterResults(
				fieldType = "input",
				inputFieldLabel = "Link",
				inputValue = "The front page of the internet");
		}

		task ("Then can see the filters apply in the list by name") {
			Testray.viewTableEntryList(listEntry = "The front page of the internet,Requirement 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should be able to filter requirements by summary"
	@priority = 3
	test AdminCanFilterRequirementsBySummary {
		task ("When the user filter a requirement by summary") {
			TestrayFilter.filterResults(
				fieldType = "input",
				inputFieldLabel = "Summary",
				inputValue = "Requirement 1");
		}

		task ("Then can see the filters apply in the list by summary") {
			Testray.viewTableEntryList(listEntry = "Requirement 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should be able to filter requirements by team"
	@priority = 3
	test AdminCanFilterRequirementsByTeam {
		task ("When the user filter a requirement by team") {
			TestrayFilter.filterResults(
				fieldType = "multiSelect",
				multiSelectFieldLabel = "Team",
				multiSelectValue = "Team 1");
		}

		task ("Then can see the team selected in the table") {
			Testray.viewTableEntryList(listEntry = "Team 1");
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify if as an Administrator I should be able to filter requirements using multiple filtering options"
	@priority = 3
	test AdminCanFilterRequirementsWithMultipleOptions {
		task ("When the user filter a requirement with multiple options") {
			TestrayFilter.filterResults(
				fieldType = "input,multiSelect",
				inputFieldLabel = "Summary",
				inputValue = "Requirement 1",
				multiSelectFieldLabel = "Team",
				multiSelectValue = "Team 1");
		}

		task ("Then can see the multiple options in the table and do not see the requirement 2") {
			Testray.viewTableEntryList(listEntry = "The front page of the internet,Requirement 1,Team 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Requirement 2");
		}
	}

	@description = "Story ID: LPS-173519 | Test Summary: Verify that can search for a field"
	@priority = 2
	test AdminCanSearchForField {
		task ("When the user search field called link") {
			TestrayFilter.clickFilter();

			TestrayFilter.searchFilter(filterName = "Link");
		}

		task ("Then can see the field called link and do not see the field key") {
			AssertElementPresent(
				key_fieldLabel = "Link",
				locator1 = "TextInput#GENERIC_TEXT_INPUT");

			AssertElementNotPresent(
				key_fieldLabel = "Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT");
		}
	}

}