@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var componentName = "Component 1";
	var projectName = "Project 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Create a Project and team via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I am able to add Components"
	@priority = 5
	test AdminCanAddComponent {
		task ("Given that I go to Manage Component") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I create a Component") {
			TestrayManage.createComponent(
				componentName = ${componentName},
				teamName = ${teamName});
		}

		task ("Then the component should appear on the list") {
			Testray.viewTableEntryList(listEntry = ${componentName});
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I should be able to add a component that does not have a team"
	@priority = 5
	test AdminCanAddComponentWithoutTeam {
		task ("Given I go to Manage Component") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I add a new component without adding a team") {
			TestrayManage.createComponent(componentName = ${componentName});
		}

		task ("Then I assert that the component was created without a team") {
			Testray.viewTableEntryList(listEntry = ${componentName});

			Testray.viewTableEntryListNotPresent(listEntry = ${teamName});
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify that as an Administrator I am able to edit Components while connecting a Team"
	@priority = 5
	test AdminCanAddTeamToComponent {
		task ("Given that I create a new Component via API") {
			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("When I add a new Component without a Team") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});

			TestrayManage.createComponent(componentName = ${componentName});
		}

		task ("Then I should be able to edit the component and add a Team") {
			TestrayManage.editComponent(
				newComponentName = "Component 1 Edited",
				newTeamName = ${teamName},
				tableEntry = ${componentName});

			Testray.viewTableEntryList(listEntry = "${teamName},Component 1 Edited");
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an administrator I can change the team assigned to the Component"
	@priority = 3
	test AdminCanChangeTeamToComponent {
		task ("Given that I create a new Team and a new Component via API") {
			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = "Team 2");

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("And I go to the Manage Components") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I change the component team") {
			TestrayManage.changeTeamToComponent(
				newTeamName = "Team 2",
				tableEntry = ${teamName});
		}

		task ("Then I assert that the Team has been changed") {
			Testray.viewTableEntryList(listEntry = "Team 2");
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I should be able to delete a component that does not have a team"
	@priority = 3
	test AdminCanDeleteComponentWithoutTeam {
		task ("Given I go to Manage Component") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I add a new component without adding a team") {
			TestrayManage.createComponent(componentName = ${componentName});

			Testray.viewTableEntryListNotPresent(listEntry = ${teamName});
		}

		task ("And I should be able to delete the component") {
			Testray.deleteItem(itemName = ${componentName});
		}

		task ("Then I assert that the component was deleted") {
			Testray.viewTableEntryListNotPresent(listEntry = ${componentName});
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I should be able to add a Component that has a Team"
	@priority = 5
	test AdminCanDeleteComponentWithTeam {
		task ("Given that I create a new Component via API") {
			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("When I go to Manage Component") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("And I assert that the component was created") {
			Testray.viewTableEntryList(listEntry = ${componentName});
		}

		task ("Then I delete the created component and I state that it does not appear in the list") {
			Testray.deleteItem(itemName = ${componentName});

			Testray.viewTableEntryListNotPresent(listEntry = ${componentName});
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify that as an Administrator I can edit Components"
	@priority = 4
	test AdminCanEditComponent {
		task ("Given that I create a new Team and a new Component via API") {
			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("And I go to the created Project") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I go to Manage Components and edit the Component") {
			Testray.viewTableEntryList(listEntry = ${componentName});

			TestrayManage.editComponent(
				newComponentName = "Component Edited",
				tableEntry = ${componentName});
		}

		task ("Then I assert that the component was edited") {
			Testray.viewTableEntryList(listEntry = "Component Edited");
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate component"
	@priority = 3
	test AdminCanNotAddDuplicatedComponent {
		task ("Given that I create a new Component via API") {
			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("When I go to Manage Component and try to add a component with a duplicate name") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});

			TestrayManage.createComponent(componentName = ${componentName});
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Component name already exists");
		}
	}

	@description = "Story ID: LPS-177839 | Test Summary: Verify if as an Administrator when I search for a Component the system should return that component"
	@priority = 4
	test AdminCanSearchComponent {
		task ("Given that two component are created via API") {
			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = "Component 2",
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("And go to the manage components") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("And confirm that there are two components in the table") {
			Testray.viewTableEntryList(listEntry = "${componentName},Component 2");
		}

		task ("When the user filter component 1") {
			TestrayFilter.filterResults(
				fieldType = "input",
				inputFieldLabel = "Component Name",
				inputValue = ${componentName});
		}

		task ("Then the user see only component 1 in the table") {
			Testray.viewTableEntryList(listEntry = ${componentName});
		}

		task ("And do not see the component 2 in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Component 2");
		}

		task ("Delete the component") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "components",
				objectName = "Component 2");
		}
	}

	@description = "Story ID: LPS-177839 | Test Summary: Verify if as an Administrator when I search for a Component that does not exist the system should not return that component"
	@priority = 3
	test AdminCanSearchComponentNonExistent {
		task ("Given that component are created via API") {
			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});
		}

		task ("And go to the manage components") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When the user search component non existent") {
			TestrayFilter.filterResults(
				fieldType = "input",
				inputFieldLabel = "Component Name",
				inputValue = "component 3");
		}

		task ("Then not see the component 3 in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Component 3");
		}
	}

	@description = "Story ID: LPS-159528 | Test Summary: Verify if as an Administrator I can see the warning message 'This field is required' when trying to save without a name"
	@priority = 2
	test CannotAddComponentWithoutAName {
		task ("Given I go to the Manage Component page") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});
		}

		task ("When I try to add a component without a name") {
			TestrayManage.createComponent();
		}

		task ("Then I assert that the 'Name' field is required") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

}