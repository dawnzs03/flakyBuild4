@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var projectName = "Project 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("Go to Testray Site") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-165583 | Test Summary: Verify if as an Administrator I am able to add a Project"
	@priority = 5
	test AdminCanAddProject {
		task ("When the administrator Create a Project") {
			TestrayProject.createProject(
				description = "Project Description",
				projectName = "Project 1");
		}

		task ("Then the administrator can see the project in the list") {
			Testray.changePagination(quantity = 150);

			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify that as an Administrator I am able to add a Project from the dropdown"
	@priority = 3
	test AdminCanAddProjectFromDropdown {
		task ("When the administrator create a Project from the dropdown") {
			TestrayProject.createProjectByDropdown(
				description = "Project Description",
				projectName = "Project 1");
		}

		task ("Then the administrator can see the project in the list") {
			Testray.changePagination(quantity = 150);

			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I am able to delete a Project"
	@priority = 5
	test AdminCanDeleteProject {
		task ("Given that the Project is create via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});
		}

		task ("When the project is deleted") {
			TestrayProject.deleteProject(projectName = "Project 1");
		}

		task ("Then the project is not displayed anymore") {
			Testray.viewTableEntryListNotPresent(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I am able to edit a Project"
	@priority = 5
	test AdminCanEditProject {
		task ("Given that the administrator create a Project") {
			TestrayProject.createProject(
				description = "Project Description",
				projectName = "Project one");
		}

		task ("When the administrator edits the created project") {
			Testray.changePagination(quantity = 150);

			TestrayManage.selectTableRowOption(
				rowOption = "Edit",
				tableEntry = "Project one");

			TestrayProject.editProject(
				newDescription = "Project Description Edited",
				newName = "Project 1");

			Testray.changePagination(quantity = 150);
		}

		task ("Then the project are presented with details edited") {
			Testray.viewTableEntryList(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should not be able to add a duplicate Project"
	@priority = 3
	test AdminCanNotAddDuplicateProject {
		task ("When the administrator create a duplicate project") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			TestrayProject.createProject(
				description = "Project Description",
				projectName = ${projectName});
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Project name already exists");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify that the user can not create a project if all the required fields are not filled in"
	@priority = 2
	test AdminCanViewWarningMessage {
		task ("When the administrator tries to create a project without a name") {
			TestrayProject.createProject();
		}

		task ("Then an error message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should be able to delete a project that contains routines without associated builds"
	@priority = 3
	test CanDeleteProjectsThatContainRoutinesWithoutAssociatedBuilds {
		task ("Given that the administrator create a Project without associated builds") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			TestrayRoutines.createRoutine(routineName = "Routine 1");
		}

		task ("And the administrator does not see any associated build") {
			Testray.clickTableEntry(tableEntry = "Routine 1");

			Testray.viewTableEntryListNotPresent(listEntry = "Build 1");

			Testray.clickBreadcrumb(text = ${projectName});
		}

		task ("When the project is deleted") {
			TestrayProject.deleteProject(projectName = "Project 1");
		}

		task ("Then the project is not displayed anymore") {
			Testray.viewTableEntryListNotPresent(listEntry = "Project 1");
		}
	}

	@description = "Story ID: LPS-146927 | Test Summary: Verify if as an Administrator I should not be able to delete a project that contains routines with associated builds"
	@priority = 4
	test CannotDeleteProjectsThatContainRoutinesWithAssociatedBuilds {
		task ("Given that the Project is create via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			Testray.changePagination(quantity = 150);
		}

		task ("And add a new team") {
			TestrayManage.selectTableRowOption(
				rowOption = "Manage Teams",
				tableEntry = ${projectName});

			TestrayManage.createTeam(teamName = "Team 1");

			Testray.closeModal(modalTitle = "Teams - Project 1");
		}

		task ("And add a new component") {
			TestrayManage.selectTableRowOption(
				rowOption = "Manage Components",
				tableEntry = ${projectName});

			TestrayManage.createComponent(
				componentName = "Component 1",
				teamName = "Team 1");

			Testray.closeModal(modalTitle = "Components - Project 1");
		}

		task ("And create a new Routine and a new Case") {
			Testray.clickTableEntry(tableEntry = ${projectName});

			TestrayRoutines.createRoutine(routineName = "Routine 1");

			Navigator.gotoNavTab(navTab = "Cases");

			TestrayCases.createCases(
				caseName = "Case 1",
				caseType = "Manual Test",
				component = "Component 1",
				description = "Case Description",
				duration = 40,
				priority = 5,
				steps = "Auto Test Steps");

			Navigator.gotoNavTab(navTab = "Routines");
		}

		task ("And add a new build with enviroments") {
			TestrayManage.selectTableRowOption(
				rowOption = "Select Default Environment Factors",
				tableEntry = "Routine 1");

			Testray.selectDefaultEnvironmentFactors(
				applicationServer = "Tomcat 6.0",
				arrowDirection = "Right",
				browser = "Chrome 57",
				listCategory = "Application Server,Browser");

			Testray.clickTableEntry(tableEntry = "Routine 1");

			TestrayBuild.addBuildViaUI(
				addVersion = "1.2",
				buildFieldName = "Build 1",
				caseList = "Case 1",
				description = "Build Description",
				dropdownItem = "New Build",
				gitHash = 12345,
				productVersionOption = "1.2",
				routine = "Routine 1");

			Testray.viewTableEntryList(listEntry = "Build 1");
		}

		task ("When the administrator delete a project with associated build") {
			TestrayProject.deleteProject(projectName = ${projectName});
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Project cannot be deleted because it has associated build");
		}
	}

}