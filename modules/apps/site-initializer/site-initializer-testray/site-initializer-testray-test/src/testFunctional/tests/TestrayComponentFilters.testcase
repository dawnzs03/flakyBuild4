@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	var buildName = "Build 1";
	var caseName = "Case 1";
	var caseResultName = "case result 1";
	var caseType = "Case Type 1";
	var componentName = "Component 1";
	var environmentFactorsName = "Environment Factors 1";
	var productVersion = "1.2";
	var projectName = "Project 1";
	var routineName = "Routine 1";
	var runName = "Option 1";
	var teamName = "Team 1";
	var testSiteName = "Testray 2";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Testray Site") {
			HeadlessSite.addSite(
				siteName = ${testSiteName},
				templateName = "Testray",
				templateType = "Site Initializer");
		}

		task ("create project, routine, team, component, case, case type, enviroment factor, product version, build, category, option, run and case result via API") {
			JSONTestray.addProject(
				projectDescription = "Project Description 1",
				projectName = ${projectName});

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = "Team 1");

			JSONTestray.addTeam(
				projectName = ${projectName},
				teamName = "Team 2");

			JSONTestray.addComponent(
				componentName = ${componentName},
				projectName = ${projectName},
				teamName = ${teamName});

			JSONTestray.addComponent(
				componentName = "Component 2",
				projectName = ${projectName},
				teamName = "Team 2");

			JSONTestray.addRoutine(
				projectName = ${projectName},
				routineName = "Routine 1");

			JSONTestray.addCaseType(caseType = ${caseType});

			JSONTestray.addCases(
				caseName = ${caseName},
				caseType = ${caseType},
				componentName = ${componentName},
				priority = 5,
				projectName = ${projectName});

			JSONTestray.addCaseType(caseType = "Case Type 2");

			JSONTestray.addCases(
				caseName = "Case 2",
				caseType = "Case Type 2",
				componentName = "Component 2",
				projectName = ${projectName});

			JSONTestray.addEnvironmentFactors(environmentFactorsName = ${environmentFactorsName});

			JSONTestray.addProductVersion(
				productVersion = "7.0",
				projectName = ${projectName});

			JSONTestray.addBuild(
				buildName = ${buildName},
				productVersion = "7.0",
				projectName = ${projectName},
				routineName = ${routineName});

			JSONTestray.addRun(
				buildName = ${buildName},
				runName = ${runName});

			JSONTestray.addRun(
				buildName = ${buildName},
				runName = "Option 2");

			JSONTestray.addCaseResult(
				buildName = ${buildName},
				caseName = ${caseName},
				caseResultName = "case result 1",
				componentName = ${componentName},
				messageError = "Failed to run on CI",
				runName = ${runName});

			JSONTestray.addCaseResult(
				buildName = ${buildName},
				caseName = "Case 2",
				caseResultName = "case result 2",
				componentName = "Component 2",
				messageError = "Aborted prior to running test",
				runName = "Option 2");
		}

		task ("Go to the component tab") {
			ApplicationsMenu.gotoSite(site = ${testSiteName});

			Testray.changePagination(quantity = 150);

			Testray.clickTableEntry(tableEntry = ${projectName});

			Testray.clickTableEntry(tableEntry = ${routineName});

			Testray.clickTableEntry(tableEntry = ${buildName});

			Navigator.gotoNavTab(navTab = "Components");
		}
	}

	tearDown {
		var testLiferayVirtualInstance = PropsUtil.get("test.liferay.virtual.instance");

		task ("Delete product version, routine, case type, factor categorie and project via API, Testray Site and Client Extensions") {
			JSONTestrayAPI.deleteObjectViaAPI(
				object = "productversions",
				objectName = "7.0");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "routines",
				objectName = "Routine 1");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = "Case Type 1");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "casetypes",
				objectName = "Case Type 2");

			JSONTestrayAPI.deleteObjectViaAPI(
				object = "factorcategories",
				objectName = "Category 1");

			JSONTestrayAPI.deleteProject(projectName = ${projectName});

			Testray.testrayApisTearDown();
		}

		if (${testLiferayVirtualInstance} == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "Story ID: LPS-173528 | Test Summary: Verify if as an Administrator I should be able to filter component by run"
	@priority = 3
	test AdminCanFilterComponentByRun {
		task ("Given that the user has two component different in the table") {
			Testray.viewTableEntryList(listEntry = "Component 1,Component 2");
		}

		task ("When the user filter a component by team") {
			TestrayFilter.filterResults(
				fieldType = "select",
				selectFieldLabel = "Run",
				selectValue = 00);
		}

		task ("Then the user see only component 1 in the table related to run 1") {
			Testray.viewTableEntryList(listEntry = "Component 1");
		}

		task ("And do not see the component 2 in the table related to run 2") {
			Testray.viewTableEntryListNotPresent(listEntry = "Component 2");
		}
	}

	@description = "Story ID: LPS-173528 | Test Summary: Verify if as an Administrator I should be able to filter component by team name"
	@priority = 3
	test AdminCanFilterComponentByTeamName {
		task ("Given that the user has two component different in the table") {
			Testray.viewTableEntryList(listEntry = "Component 1,Component 2");
		}

		task ("When the user filter a component by team") {
			TestrayFilter.filterResults(
				fieldType = "multiSelect",
				multiSelectFieldLabel = "Team",
				multiSelectValue = "Team 1");
		}

		task ("Then the user see only component 1 in the table related to team 1") {
			Testray.viewTableEntryList(listEntry = "Component 1");
		}

		task ("And do not see the component 2 in the table related to team 2") {
			Testray.viewTableEntryListNotPresent(listEntry = "Component 2");
		}
	}

	@description = "Story ID: LPS-173528 | Test Summary: Verify if as an Administrator I should be able to filter component using multiple filtering options"
	@priority = 4
	test AdminCanFilterComponentWithMultipleOptions {
		task ("Given that the user has two component different in the table") {
			Testray.viewTableEntryList(listEntry = "Component 1,Component 2");
		}

		task ("When the user filter component using multiple filtering options") {
			TestrayFilter.filterResults(
				fieldType = "multiSelect,select",
				multiSelectFieldLabel = "Team",
				multiSelectValue = "Team 1",
				selectFieldLabel = "Run",
				selectValue = 00);
		}

		task ("Then the user see only component 1 in the table") {
			Testray.viewTableEntryList(listEntry = "Component 1");
		}

		task ("And do not see the component 2 in the table") {
			Testray.viewTableEntryListNotPresent(listEntry = "Component 2");
		}
	}

	@description = "Story ID: LPS-173528 | Test Summary: Verify if as an User I should be able to search by a filter"
	@priority = 2
	test CanSearchByFilter {
		task ("When the user searches for a field called priority") {
			TestrayFilter.clickFilter();

			ScrollWebElementIntoView(
				key_tableEntry = "Component 2",
				locator1 = "Testray#TABLE_ENTRY");

			TestrayFilter.searchFilter(filterName = "Priority");
		}

		task ("Then the user can see the field label called priority") {
			AssertElementPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Priority");
		}

		task ("And the user no more see the field called Run in the filter") {
			AssertElementNotPresent(
				locator1 = "Select#SELECT_FIELD_LABEL",
				selectFieldLabel = "Run");
		}
	}

}